# Элементы анимации

Существует несколько типов анимационных элементов, каждый из которых оптимизирован для конкретного случая использования. Ниже приведен список наиболее распространенных видов анимации:
* `PropertyAnimation` - Анимация изменения значений свойств
* `NumberAnimation` - Анимация изменений значений типа `qreal`
* `ColorAnimation` - Анимация изменения значений цвета
* `RotationAnimation` - Анимация изменения значений поворота
Помимо этих базовых и широко используемых элементов анимации, Qt Quick предоставляет и более специализированные анимации для конкретных случаев использования:
* `PauseAnimation` - Обеспечивает паузу для анимации
* `SequentialAnimation` - Позволяет запускать анимацию последовательно
* `ParallelAnimation` - Позволяет запускать анимацию параллельно
* `AnchorAnimation` - Анимация изменения значений якорей
* `ParentAnimation` - Анимация изменения родительских значений
* `SmoothedAnimation` - Позволяет свойству плавно отслеживать значение
* `SpringAnimation` - Позволяет свойству отслеживать значение в пружинящем движении
* `PathAnimation` - Анимация элемента вдоль траектории движения
* `Vector3dAnimation` - Анимация изменений значений QVector3d

При работе над более сложными анимациями иногда возникает необходимость изменить какое-либо свойство или запустить сценарий во время текущей анимации. Для этого Qt Quick предлагает элементы действия, которые можно использовать везде, где можно использовать другие элементы анимации:
* `PropertyAction` - задает немедленное изменение свойств во время анимации
* `ScriptAction` - Определяет сценарии, запускаемые во время анимации
## Применение анимации
Анимация может применяться несколькими способами:
* **Анимация на свойстве** - запускается автоматически после полной загрузки элемента
* **Поведение при изменении свойства** - выполняется автоматически при изменении значения свойства.
* **Автономная анимация** - запускается, когда анимация явно запущена с помощью функции `start()` или для параметра `running` установлено значение `true` (например, с помощью привязки свойства).
## Кликабельное изображение V2
Для демонстрации использования анимации мы повторили наш компонент `ClickableImage` из предыдущей главы и дополнили его текстовым элементом.
```c++
// ClickableImageV2.qml
// Simple image which can be clicked

import QtQuick

Item {
	id: root
	
	width: container.childrenRect.width
	height: container.childrenRect.height
	
	property alias text: label.text
	property alias source: image.source
	
	signal clicked
	
	Column {
		id: container
		
		Image {
			id: image
		}

		Text {
			id: label
			
			width: image.width
			horizontalAlignment: Text.AlignHCenter
			
			wrapMode: Text.WordWrap
			color: "#ececec"
		}
	}
	
	MouseArea {
		anchors.fill: parent
		onClicked: root.clicked()
	}
}
```
Для организации элемента под изображением мы использовали позиционер `Column` и вычислили ширину и высоту на основе свойства `childrenRect` колонки. Мы открыли свойства источника текста и изображения, а также сигнал щелчка. Мы также хотели, чтобы текст был такой же ширины, как и изображение, и чтобы он обворачивался. Для этого мы используем свойство `wrapMode` элемента `Text`.

> * **Геометрическая зависимость "родитель/ребенок**
>   Из-за инверсии геометрической зависимости (родительская геометрия зависит от дочерней) мы не можем задать `ширину/высоту` для ClickableImageV2, так как это нарушит нашу привязку `ширины/высоты`.
>   Предпочтительнее, чтобы геометрия дочернего элемента зависела от геометрии родительского элемента, если элемент является скорее контейнером для других элементов и должен адаптироваться к геометрии родительского элемента.

## Объекты по возрастанию
Три объекта находятся в одном и том же положении по оси `y` (`y=200`). Им всем нужно добраться до `y=40`, причем каждый из них использует свой метод с различными побочными эффектами и возможностями.
### Первый объект
Первый объект перемещается с использованием стратегии `Animation on <property>`. Анимация начинается немедленно.
```c++
ClickableImageV2 {
	id: greenBox
	x: 40; y: root.height-height
	
	source: "assets/box_green.png"
	
	text: qsTr("animation on property")
	
	NumberAnimation on y {
		to: 40; duration: 4000
	}
}
```
При щелчке на объекте его положение по оси `y` сбрасывается в начальное положение, причем это происходит со всеми объектами. Для первого объекта сброс не имеет значения пока работает анимация, не имеет никакого эффекта.
Это может быть визуально неприятно, так как за долю секунды до начала анимации y-позиция устанавливается в новое значение. Таких конкурирующих изменений свойства следует избегать.
### Второй объект
Второй объект путешествует, используя поведение анимации. Это поведение указывает свойству, что оно должно анимировать каждое изменение значения. Поведение можно отключить, установив для элемента `Behavior` значение `enabled: false`.
```c++
ClickableImageV2 {
	id: blueBox
	
	x: (root.width-width)/2; y: root.height-height
	
	source: "assets/box_blue.png"
	
	text: qsTr("behavior on property")
	
	Behavior on y {
		NumberAnimation { duration: 4000 }
	}
	
	onClicked: y = 40
	// random y on each click
	// onClicked: y = 40 + Math.random() * (205-40)
}
```
Объект начнет перемещаться, когда вы щелкнете на нем (его положение по оси `y` будет установлено на 40). Повторный щелчок не оказывает никакого влияния, так как положение уже установлено.
Можно попробовать использовать случайное значение (например, `40 + (Math.random()* (205- 40))` для позиции `y`. Вы увидите, что объект всегда будет анимироваться в новое положение и адаптировать свою скорость, чтобы соответствовать 4 секундам до места назначения, определенным длительностью анимации.
### Третий объект
Третий объект использует автономную анимацию. Анимация определяется как собственный элемент и может находиться практически в любом месте документа.
```c++
ClickableImageV2 {
	id: redBox
	
	x: root.width-width-40; y: root.height-height
	
	source: "assets/box_red.png"
	
	onClicked: anim.start()
	// onClicked: anim.restart()

	text: qsTr("standalone animation")
	
	NumberAnimation {
		id: anim
		target: redBox
		properties: "y"
		to: 40
		duration: 4000
	}
}
```
Щелчок запускает анимацию с помощью функции `start()` анимации. Каждая анимация имеет функции `start()`, `stop()`, `resume()` и `restart()`. Сама анимация содержит гораздо больше информации, чем другие типы анимации, рассмотренные ранее.
Нам необходимо определить `target` - элемент, который будет анимирован, а также имена свойств, которые мы хотим анимировать. Также необходимо определить значение `to` и, в данном случае, значение `from`, которое позволяет перезапустить анимацию.
![[QML Animation.png]]
Щелчок на фоне приводит к сбросу всех объектов в исходное положение. Первый объект не может быть перезапущен, кроме как путем повторного запуска программы, вызывающей повторную загрузку элемента.
> Другим способом `запуска/остановки` анимации является привязка свойства к выполняющемуся свойству анимации. Это особенно удобно, когда пользовательский ввод управляет свойствами:
```c++
NumberAnimation {
	// [...]
	// animation runs when mouse is pressed
	running: area.pressed
}
MouseArea {
	id: area
}
```
## Смягчение кривых
Изменением значения свойства можно управлять с помощью анимации. Атрибуты `Easing` позволяют влиять на интерполяционную кривую изменения свойства.
Все анимации, которые мы определили к настоящему времени, используют линейную интерполяцию, поскольку исходным типом смягчения анимации является `Easing.Linear`. Лучше всего это представить на небольшом графике, где ось `y` - анимируемое
свойство, а ось `x` - время (длительность). Линейная интерполяция проведет прямую линию от значения `from` в начале анимации до значения `to` в конце анимации. Таким образом, тип `easing` определяет кривую изменения .
Типы смягчения должны быть тщательно подобраны, чтобы поддерживать естественное соответствие движущемуся объекту. Например, когда страница выдвигается, она должна сначала выдвигаться медленно, а затем набирать обороты и в конце концов выдвигаться с большой скоростью, подобно перелистыванию страницы книги.
> Как и в других аспектах дизайна пользовательского интерфейса, анимация должна быть тщательно продумана, чтобы поддерживать поток пользовательского интерфейса, а не доминировать над ним. Глаз очень чувствителен к движущимся объектам, и анимация может легко отвлечь пользователя.

В следующем примере мы попробуем использовать несколько кривых смягчения. Каждая кривая смягчения отображается в виде кликабельного изображения и при нажатии на нее устанавливает новый тип смягчения для анимации квадрата , а затем запускает функцию `restart()` для запуска анимации с новой кривой.
![[QML curve.png]]
Код для этого примера был немного усложнен. Сначала мы создаем сетку типов `EasingTypes` и бокс, который управляется типами `easing`. Тип смягчения просто отображает кривую, которую бокс должен использовать для своей анимации. Когда пользователь щелкает на кривой смягчения, бокс перемещается в направлении, соответствующем кривой смягчения. Сама анимация представляет собой отдельную анимацию с целью, установленной на коробку, и настроенной на анимацию свойства `x` с длительностью 2 секунды.
> Внутреннее устройство `EasingType` выводит кривую в реальном времени, и  заинтересованный читатель может посмотреть это в примере `EasingCurves`.
```c++
// EasingCurves.qml
import QtQuick
import QtQuick.Layouts

Rectangle {
	id: root
	
	width: childrenRect.width
	height: childrenRect.height

	color: '#4a4a4a'
	gradient: Gradient {
		GradientStop { position: 0.0; color: root.color }
		GradientStop { position: 1.0; color: Qt.lighter(root.color)}
	}

	ColumnLayout {
		Grid {
			spacing: 8
			columns: 5
			
			EasingType {
				easingType: Easing.Linear
				title: 'Linear'
				
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.InExpo
				title: "InExpo"
				
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.OutExpo
				title: "OutExpo"
					
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.InOutExpo
				title: "InOutExpo"
				
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.InOutCubic
				title: "InOutCubic"
				
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.SineCurve
				title: "SineCurve"
			
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.InOutCirc
				title: "InOutCirc"
				
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.InOutElastic
				title: "InOutElastic"
	
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.InOutBack
				title: "InOutBack"
				
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
			
			EasingType {
				easingType: Easing.InOutBounce
				title: "InOutBounce"
				
				onClicked: {
					animation.easing.type = easingType
					box.toggle = !box.toggle
				}
			}
		}
		
		Item {
			height: 80
			
			Layout.fillWidth: true
			
			Box {
				id: box
				
				property bool toggle

				x: toggle ? 20 : root.width - width - 20
				
				anchors.verticalCenter: parent.verticalCenter
				
				gradient: Gradient {
					GradientStop { position: 0.0; color: "#2ed5"}
					GradientStop { position: 1.0; color: "#2467"}
				}
				
				Behavior on x {
					NumberAnimation {
						id: animation
						duration: 500
					}
				}
			}
		}
	}
}
```
Помимо свойств `duration` и `easing.type`, существует возможность тонкой настройки анимации. Например, общий тип `PropertyAnimation` (от которого наследуется большинство анимаций) дополнительно поддерживает свойства `easing.amplitude`, `easing.overshoot` и `easing.period`, позволяющие тонко настраивать поведение определенных кривых ослабления.
Не все кривые ослабления поддерживают эти параметры. Чтобы проверить, влияет ли тот или иной параметр смягчения на кривую смягчения, обратитесь к таблице смягчения из документации `PropertyAnimation`.
> Выбор правильной анимации для элемента в контексте пользовательского интерфейса имеет решающее значение для результата. Помните, что анимация должна поддерживать работу пользовательского интерфейса, а не раздражать его.
## Сгруппированные анимации
Часто анимация оказывается более сложной, чем просто анимация одного свойства. Возможно, потребуется запустить несколько анимаций одновременно или одну за другой, или даже выполнить сценарий между двумя анимациями. 
Для этого можно использовать сгруппированные анимации. Как следует из названия, анимации можно группировать. Группировка может осуществляться двумя способами: параллельным или последовательным. Для этого можно использовать элемент `SequentialAnimation` или `ParallelAnimation`, которые выступают в роли контейнеров анимации для других элементов анимации. Эти сгруппированные анимации сами являются анимациями и могут использоваться именно как таковые.
![[QML Group Animation.png]]
```c++
// ParallelAnimationExample.qml

import QtQuick

BrightSquare {
	id: root

	property int duration: 3000
	property Item ufo: ufo
	
	width: 600
	height: 400
	
	Image {
		anchors.fill: parent
		source: "assets/ufo_background.png"
	}
	
	ClickableImageV3 {
		id: ufo
		
		x: 20; y: root.height-height
		
		text: qsTr('ufo')
		
		source: "assets/ufo.png"
		
		onClicked: anim.restart()
	}

	ParallelAnimation {
		id: anim
		
		NumberAnimation {
			target: ufo
			properties: "y"
			to: 20
			duration: root.duration
		}
		
		NumberAnimation {
			target: ufo
			properties: "x"
			to: 160
			duration: root.duration
		}
	}
}
```
![[QML Group Animation_2.png]]
## Последовательная анимация
При последовательной анимации каждая дочерняя анимация запускается в том порядке, в котором она объявлена: сверху вниз.
```c++
// SequentialAnimationExample.qml

import QtQuick

BrightSquare {
	id: root
	
	property int duration: 3000
	property Item ufo: ufo

	width: 600
	height: 400
	
	Image {
		anchors.fill: parent
		source: "assets/ufo_background.png"
	}
	
	ClickableImageV3 {
		id: ufo
		x: 20; y: root.height-height
		
		text: qsTr('rocket')
		
		source: "assets/ufo.png"
		
		onClicked: anim.restart()
	}
	
	SequentialAnimation {
		id: anim
		
		NumberAnimation {
			target: ufo
			properties: "y"
			to: 20
			// 60% of time to travel up
			duration: root.duration * 0.6
		}
		
		NumberAnimation {
			target: ufo
			properties: "x"
			to: 400
			// 40% of time to travel sideways
			duration: root.duration * 0.4
		}
	}
}
```
![[QML Animation_3.png]]
## Вложенные анимации
Сгруппированные анимации также могут быть вложенными. Например, последовательная анимация может иметь две параллельные анимации в качестве дочерних анимаций и т.д. Мы можем представить это на примере футбольного мяча. Идея состоит в том, чтобы бросить мяч слева направо и анимировать его поведение.
![[QML Animation_4.png]]
Для понимания анимации необходимо расчленить ее на целостные преобразования объекта. Необходимо помнить, что анимация анимирует изменения свойств. Вот различные преобразования:
>* Х-трансляция слева направо (X1)
>* Перемещение по оси `Y` снизу вверх (Y1) с последующим перемещением сверху вниз (Y2) с некоторым подпрыгиванием
>* Вращение на 360 градусов в течение всего времени анимации (ROT1)

Вся продолжительность анимации должна занимать три секунды.
![[QML Animation_5.png]]
Мы начинаем с пустого элемента в качестве корневого элемента шириной 480 и высотой 300.
```c++
import QtQuick
Item {
	id: root
	
	property int duration: 3000
	
	width: 480
	height: 300
	
	// [...]
}
```
Мы определили общую продолжительность анимации в качестве эталона для лучшей синхронизации частей анимации. Следующий шаг - добавление фона, который в нашем случае представляет собой 2 прямоугольника с зеленым и синим градиентами.
```c++
Rectangle {
	id: sky

	width: parent.width
	height: 200
	
	gradient: Gradient {
		GradientStop { position: 0.0; color: "#0080FF" }
		GradientStop { position: 1.0; color: "#66CCFF" }
	}
}

Rectangle {
	id: ground
	
	anchors.top: sky.bottom
	anchors.bottom: root.bottom
	width: parent.width
	
	gradient: Gradient {
		GradientStop { position: 0.0; color: "#00FF00" }
		GradientStop { position: 1.0; color: "#00803F" }
	}
}
```
![[QML Animation_7.png]]
Верхний синий прямоугольник занимает 200 пикселей высоты, а нижний привязан к нижней части неба и к нижней части корневого элемента.
Переносим футбольный мяч на зеленую площадку. Мяч представляет собой изображение, хранящееся в папке "assets/soccer_ball.png". Для начала расположим его в левом нижнем углу, рядом с краем.
```c++
Image {
	id: ball
	
	x: 0; y: root.height-height
	
	source: "assets/soccer_ball.png"
	
	MouseArea {
		anchors.fill: parent

		onClicked: {
			ball.x = 0
			ball.y = root.height-ball.height
			
			ball.rotation = 0
			
			anim.restart()
		}
	}
}
```
![[QML Animation_8.png]]
К изображению прикреплена область мыши. Если щелкнуть мышью по шарику, то его положение изменится и анимация будет перезапущена.
Начнем с последовательной анимации для двух трансляций `y`.
```c++
SequentialAnimation {
	id: anim
	
	NumberAnimation {
		target: ball
		properties: "y"
		to: 20
		duration: root.duration * 0.4
	}
	
	NumberAnimation {
		target: ball
		properties: "y"
		to: 240
		duration: root.duration * 0.6
	}
}
```
![[QML Animation_9.png]]
Это означает, что 40% от общей продолжительности анимации занимает анимация подъема, а 60% - анимация опускания, причем каждая анимация выполняется последовательно. Трансформации анимируются по линейной траектории, но на данный момент кривые отсутствуют. Кривые будут добавлены позже с помощью кривых смягчения, в данный момент мы сосредоточены на анимации трансформаций.
Далее необходимо добавить x-трансляцию. x-трансляция должна выполняться параллельно с y-трансляцией, поэтому нам необходимо заключить последовательность y-трансляций в параллельную анимацию вместе с x-трансляцией.
```c++
ParallelAnimation {
	id: anim
	
	SequentialAnimation {
		// ... our Y1, Y2 animation
	}
	
	NumberAnimation { // X1 animation
		target: ball
		properties: "x"
		to: 400
		duration: root.duration
	}
}
```
![[QML Animation_10.png]]
В итоге мы хотим, чтобы шар вращался. Для этого к параллельной анимации нужно добавить еще одну анимацию. Мы выбираем `RotationAnimation`, так как она специализирована для вращения.
```c++
ParallelAnimation {
	id: anim
	
	SequentialAnimation {
		// ... our Y1, Y2 animation
	}
	
	NumberAnimation { // X1 animation
		// X1 animation
	}
	
	RotationAnimation {
		target: ball
		properties: "rotation"
		to: 720
		duration: root.duration
	}
}
```
Вот и вся анимационная последовательность. Осталось только задать правильные кривые смягчения для движений шарика. Для анимации **Y1** мы используем кривую `Easing.OutCirc`, так как это должно быть больше похоже на круговое движение. **Y2** используется `Easing.OutBounce`, чтобы придать мячу отскок, причем отскок должен происходить в конце (попробуйте использовать `Easing.InBounce`, и вы увидите, что отскок начинается сразу).
Анимации **X1** и **ROT1** оставлены как есть, с линейной кривой. Ниже приведен окончательный код анимации для ознакомления
```c++
ParallelAnimation {
	id: anim
	
	SequentialAnimation {

		NumberAnimation {
			target: ball
			properties: "y"
			to: 20
			duration: root.duration * 0.4
			easing.type: Easing.OutCirc
		}
		
		NumberAnimation {
			target: ball
			properties: "y"
			to: root.height-ball.height
			duration: root.duration * 0.6
			easing.type: Easing.OutBounce
		}
	}
	
	NumberAnimation {
		target: ball
		properties: "x"
		to: root.width-ball.width
		duration: root.duration
	}
	
	RotationAnimation {
		target: ball
		properties: "rotation"
		to: 720
		duration: root.duration
	}
}
```














