До сих пор мы использовали элемент `Rectangle` и элементы управления, но для создания фигур свободной формы нам приходится полагаться на изображения. С помощью модуля `Qt Quick Shapes` можно создавать фигуры действительно свободной формы. Это позволяет гибко создавать визуализации непосредственно из QML.

В этой главе мы рассмотрим, как использовать фигуры, различные доступные элементы контура, как фигуры могут быть заполнены различными способами, а также как сочетать фигуры с возможностями QML для плавной анимации фигур.

## Базовая форма
Модуль `shape` позволяет создавать произвольные контуры с последующим обводкой контура и заливкой внутренней части. Определение пути может быть повторно использовано в других местах, где используются пути, например, для элемента [[Модель-Вид-Делегат#PathView|PathView]], используемого с моделями. Но для рисования  контура используется элемент `Shape`, а различные элементы контура помещаются в `ShapePath`. В приведенном ниже примере создается контур, показанный на скриншоте. Вся фигура, все пять залитых областей, создаются из одного контура, который затем обводится штрихом и заливается.
![[QML Shape.png]]
```c++
import QtQuick
import QtQuick.Shapes

Rectangle {
	id: root
	
	width: 600
	height: 600
	
	Shape {
		anchors.centerIn: parent
		
		ShapePath {
			strokeWidth: 3
			strokeColor: "darkGray"
			fillColor: "lightGray"
			startX: -40; startY: 200
			
			// The circle
			PathArc { x: 40; y: 200; radiusX: 200; radiusY: 200
			PathLine { x: 40; y: 120 }
			PathArc { x: -40; y: 120; radiusX: 120; radiusY: 12
			PathLine { x: -40; y: 200 }
			
			// The dots
			PathMove { x: -20; y: 80 }
			PathArc { x: 20; y: 80; radiusX: 20; radiusY: 20; u
			PathArc { x: -20; y: 80; radiusX: 20; radiusY: 20;
			PathMove { x: -20; y: 130 }
			PathArc { x: 20; y: 130; radiusX: 20; radiusY: 20;
			PathArc { x: -20; y: 130; radiusX: 20; radiusY: 20;
			PathMove { x: -20; y: 180 }
			PathArc { x: 20; y: 180; radiusX: 20; radiusY: 20;
			PathArc { x: -20; y: 180; radiusX: 20; radiusY: 20;
			PathMove { x: -20; y: 230 }
			PathArc { x: 20; y: 230; radiusX: 20; radiusY: 20;
			PathArc { x: -20; y: 230; radiusX: 20; radiusY: 20;
		}
	}
}
```
Путь состоит из дочерних элементов `ShapePath`, т.е. элементов [[PathArc|PathArc]] ,
[[PathLine.md|PathLine]] и [[PathMove.md|PathMove]] в приведенном примере. В следующем разделе
мы подробно рассмотрим составные элементы контуров.

## Пути строительства

Как мы видели в предыдущем разделе, фигуры строятся из путей, которые строятся из элементов пути. Наиболее распространенным способом построения контура является его замыкание, т.е. чтобы он начинался и заканчивался в одной и той же точке. Однако можно создавать и открытые контуры, например, только для обводки. При заполнении открытого контура он закрывается прямой линией, по сути, добавляется линия [[PathLine|PathLine]] , которая используется при заполнении контура, но не при его обводке.

Как показано на рисунке ниже, существует несколько основных фигур, которые можно использовать для построения траектории. К ним относятся: линии, дуги и различные кривые. Также возможно перемещение без рисования с помощью элемента [[PathMove|PathMove]]. Помимо этих элементов, элемент [[ShapePath.md|ShapePath]] также позволяет указать начальную точку с помощью свойств `startX` и `startY`
![[QML Shape_1.png]]
Линии рисуются с помощью элемента [[PathLine|PathLine]] , как показано ниже. Для создания нескольких независимых линий можно использовать элемент [[PathMultiline|PathMultiline]].
```c++
Shape {
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		startX: 20; startY: 70

		PathLine {
			x: 180
			y: 130
		}
	}
}
```
При создании полилинии, т.е. линии, состоящей из нескольких отрезков, можно использовать элемент [[PathPolyline|PathPolyline]]. Это позволяет сэкономить на вводе текста, так как конечная точка последней линии принимается за начальную точку следующей линии.
```c++
Shape {
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		
		PathPolyline {
			
			path: [
				Qt.point(220, 100),
				Qt.point(260, 20),
				Qt.point(300, 170),
				Qt.point(340, 60),
				Qt.point(380, 100)
			]
		}
	}
}
```
Для создания дуг, т.е. сегментов окружностей или эллипсов, используются элементы [[PathArc|PathArc]] и [[PathAngleArc|PathAngleArc]] . Они предоставляют инструменты для создания дуг, причем [[PathArc|PathArc]] используется, когда известны координаты начальной и конечной точек, а [[PathAngleArc|PathAngleArc]] - когда нужно контролировать, на сколько градусов развернется дуга. Оба элемента дают одинаковый результат, поэтому выбор элемента зависит от того, какие аспекты дуги наиболее важны в вашем приложении.
```c++
Shape {
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		startX: 420; startY: 100
		
		PathArc {
			x: 580; y: 180
			radiusX: 120; radiusY: 120
		}
	}
}
```
После линий и дуг следуют различные кривые. Здесь Qt Quick Shapes предлагает три варианта. Сначала мы рассмотрим [[PathQuad|PathQuad]] , который позволяет создать квадратичную кривую Безье на основе начальной и конечной точек (начальная точка является неявной) и одной контрольной точки.
```c++
Shape {
	
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		startX: 20; startY: 300
		
		PathQuad {
			x: 180; y: 300
			controlX: 60; controlY: 250
		}
	}
}
```
Элемент [[PathCubic|PathCubic]] создает кубическую кривую Безье из начальной
и конечной точек (начальная точка является неявной) и двух контрольных точек.
```c++
Shape {
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		startX: 220; startY: 300
		
		PathCubic {
			x: 380; y: 300
			control1X: 260; control1Y: 250
			control2X: 360; control2Y: 350
		}
	}
}
```
Наконец, [[PathCurve|PathCurve]] создает кривую, проходящую через список заданных контрольных точек. Кривая создается путем предоставления нескольких элементов [[PathCurve|PathCurve]] , каждый из которых содержит одну контрольную точку. Для создания кривой, проходящей через контрольные точки, используется сплайн `Catmull-Rom`.
```c++
Shape {
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		startX: 420; startY: 300
		
		PathCurve { x: 460; y: 220 }
		PathCurve { x: 500; y: 370 }
		PathCurve { x: 540; y: 270 }
		PathCurve { x: 580; y: 300 }
	}
}
```
Существует еще один полезный элемент пути - [[PathSvg|PathSvg]] . Этот элемент позволяет обводить и заливать контур `SVG`

> * Элемент [[PathSvg|PathSvg]] не всегда можно комбинировать с другими элементами пути. Это зависит от используемого бэкенда рисования, поэтому убедитесь, что для одного пути используется элемент [[PathSvg|PathSvg]] или другие элементы. Если вы смешиваете [[PathSvg|PathSvg]] с другими элементами контура, то это зависит от ваших возможностей.

## Заполнение форм
Фигура может быть заполнена различными способами. В этом разделе мы рассмотрим общее правило заполнения, а также различные способы заполнения контура.

Qt Quick Shapes предоставляет два правила заливки, управляемые с помощью свойства `fillRule` элемента [[ShapePath|ShapePath]]. Различные результаты показаны на скриншоте ниже. Свойство может быть установлено в значение `ShapePath.OddEvenFill`, которое используется по умолчанию. При этом каждая часть контура заполняется отдельно, что означает возможность создания фигуры с отверстиями. Альтернативным правилом является `ShapePath.WindingFill`, которое заполняет все между крайними точками на каждой горизонтальной линии, пересекающей фигуру. Независимо от правила заливки контур фигуры рисуется пером, поэтому даже при использовании правила извилистой заливки контур рисуется внутри фигуры.
![[QML Shape_2.png]]
Приведенные ниже примеры демонстрируют использование двух правил заполнения, как показано на скриншоте выше.
```c++
Shape {
	
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		fillColor: "orange"
		fillRule: ShapePath.OddEvenFill
		
		PathPolyline {
			path: [
				Qt.point(100, 20),
				Qt.point(150, 180),
				Qt.point( 20, 75),
				Qt.point(180, 75),
				Qt.point( 50, 180),
				Qt.point(100, 20),
			]
		}
	}
}

Shape {

	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		fillColor: "orange"
		fillRule: ShapePath.WindingFill
		
		PathPolyline {
			path: [
				Qt.point(300, 20),
				Qt.point(350, 180),
				Qt.point(220, 75),
				Qt.point(380, 75),
				Qt.point(250, 180),
				Qt.point(300, 20),
			]
		}
	}
}
```
![[QML Shape_3.png]]
Для заливки фигуры сплошным цветом используется свойство?
```c++
Shape {
	
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		fillColor: "lightgreen"
		startX: 20; startY: 140
		
		PathLine {
			x: 180
			y: 140
		}
		
		PathArc {
			x: 20
			y: 140
			radiusX: 80
			radiusY: 80
			direction: PathArc.Counterclockwise
			useLargeArc: true
		}
	}
}
```
Если вы не хотите использовать сплошной цвет, можно применить градиент. Градиент применяется с помощью свойства `fillGradient` элемента [[ShapePath|ShapePath]].

Первый градиент, который мы рассмотрим - `LinearGradient`. Он создает линейный градиент между начальной и конечной точками. Конечные точки могут быть расположены как угодно, например, для создания градиента под углом. Между конечными точками можно вставить ряд элементов `GradientStop`. Они располагаются в диапазоне от 0.0 , что является позицией `x1`, `y1` , до 1.0 , что является позицией `x2`, `y2` . Для каждой такой остановки задается цвет. Затем градиент создает мягкие переходы между цветами.

Если форма выходит за пределы конечных точек, то первый или последний цвет либо продолжается, либо градиент повторяется или зеркально отражается. Это поведение задается с помощью свойства `spread` элемента [[LinearGradient|LinearGradient]].

```c++
Shape {

	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		
		fillGradient: LinearGradient {
			x1: 50; y1: 300
			x2: 150; y2: 280
			
			GradientStop { position: 0.0; color: "lightgreen" }
			GradientStop { position: 0.7; color: "yellow" }
			GradientStop { position: 1.0; color: "darkgreen" }
		}
	
		startX: 20; startY: 340
		
		PathLine {
			x: 180
			y: 340
		}
		
		PathArc {
			x: 20
			y: 340
			
			radiusX: 80
			radiusY: 80
			direction: PathArc.Counterclockwise
			useLargeArc: true
		}
	}
}
```
Для создания градиента, распространяющегося вокруг начала координат, напоминающего часы, используется градиент [[ConicalGradient|ConicalGradient]]. Здесь центральная точка задается с помощью свойств `centerX` и `centerY`, а начальный угол - с помощью свойства `angle`. Затем остановки градиента распространяются от заданного угла по часовой стрелке на 360 градусов.
```c++
Shape {
	
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		
		fillGradient: ConicalGradient {
			centerX: 300; centerY: 100
			angle: 45
			GradientStop { position: 0.0; color: "lightgreen" }
			GradientStop { position: 0.7; color: "yellow" }
			GradientStop { position: 1.0; color: "darkgreen" }
		}
		
		startX: 220; startY: 140

		PathLine {
			x: 380
			y: 140
		}
		
		PathArc {
			x: 220
			y: 140
			radiusX: 80
			radiusY: 80
			direction: PathArc.Counterclockwise
			useLargeArc: true
		}
	}
}
```
Для создания градиента, образующего круги, напоминающие кольца на воде, используется [[RadialGradient|RadialGradient]] . Для него задаются две окружности - фокальная и центральная. Стопы градиента идут от фокальной окружности к центральной, а за пределами этих окружностей последний цвет продолжается, зеркально отражается или повторяется, в зависимости от свойства `spread`.
```c++
Shape {
	
	ShapePath {
		strokeWidth: 3
		strokeColor: "darkgray"
		
		fillGradient: RadialGradient {
			centerX: 300; centerY: 250; centerRadius: 120
			focalX: 300; focalY: 220; focalRadius: 10
			GradientStop { position: 0.0; color: "lightgreen" }
			GradientStop { position: 0.7; color: "yellow" }
			GradientStop { position: 1.0; color: "darkgreen" }
		}
		
		startX: 220; startY: 340

		PathLine {
			x: 380
			y: 340
		}
		
		PathArc {
			x: 220
			y: 340
			radiusX: 80
			radiusY: 80
			direction: PathArc.Counterclockwise
			useLargeArc: true
		}
	}
}
```

> Опытный пользователь может использовать фрагментный шейдер для заливки фигуры. При этом предоставляется полная свобода выбора способа заливки фигуры. [[Эффекты в QML|Более подробная информация о шейдерах]] 

## Анимация фигур
Одним из приятных аспектов использования Qt Quick Shapes является то, что рисуемые контуры определяются непосредственно в QML. Это означает, что их свойства можно связывать, переходить и анимировать, как и любые другие свойства в QML.
![[QML Shape_4.png]]
В приведенном ниже примере мы используем базовую форму из самого первого раздела этой главы, но вводим переменную `t`, которая изменяется от 0,0 в 1 .0 в цикле. Затем мы используем эту переменную для смещения позиции малых кругов, а также размер верхнего и нижнего круга. Это создает анимацию, при которой кажется, что круги появляются вверху и исчезают к низу.
```c++
import QtQuick
import QtQuick.Shapes

Rectangle {
	id: root
	width: 600
	height: 600
	
	Shape {
		anchors.centerIn: parent
		
		ShapePath {
			id: shapepath
			property real t: 0.0
			
			NumberAnimation on t { from: 0.0; to: 1.0; duration
			
			strokeWidth: 3
			strokeColor: "darkGray"
			fillColor: "lightGray"
			startX: -40; startY: 200
			
			// The circle
			PathArc { x: 40; y: 200; radiusX: 200; radiusY: 200
			PathLine { x: 40; y: 120 }
			PathArc { x: -40; y: 120; radiusX: 120; radiusY: 12
			PathLine { x: -40; y: 200 }
			
			// The dots
			PathMove { x: -20+(1.0-shapepath.t)*20; y: 80 + sha
			PathArc { x: 20-(1.0-shapepath.t)*20; y: 80 + shape
			PathArc { x: -20+(1.0-shapepath.t)*20; y: 80 + shap
			PathMove { x: -20; y: 130 + shapepath.t*50 }
			PathArc { x: 20; y: 130 + shapepath.t*50; radiusX:
			PathArc { x: -20; y: 130 + shapepath.t*50; radiusX:
			PathMove { x: -20; y: 180 + shapepath.t*50 }
			PathArc { x: 20; y: 180 + shapepath.t*50; radiusX:
			PathArc { x: -20; y: 180 + shapepath.t*50; radiusX:
			PathMove { x: -20+shapepath.t*20; y: 230 + shapepat
			PathArc { x: 20-shapepath.t*20; y: 230 + shapepath.
			PathArc { x: -20+shapepath.t*20; y: 230 + shapepath
		}
	}
}
```
Обратите внимание, что вместо `NumberAnimation` для `t` можно использовать любую другую привязку, например, к ползунку, внешнему состоянию и т.д. Воображение не ограничивается.




















