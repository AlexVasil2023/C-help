# Программа просмотра изображений
Рассмотрим более крупный пример использования Qt Quick Controls. Для этого мы создадим простую программу просмотра изображений. Сначала мы создадим его для настольных компьютеров, используя стиль `Fusion`, затем рефакторим его для мобильных устройств, после чего посмотрим на финальную кодовую базу.
## Настольная версия
Настольная версия основана на классическом окне приложения со строкой меню, панелью инструментов и областью документов. В действии это приложение можно увидеть ниже.
![[QML ImageViewer.png]]

В качестве отправной точки мы используем шаблон проекта Qt Creator для пустого приложения Qt Quick. Однако элемент Window по умолчанию из шаблона мы заменим на ApplicationWindow из модуля QtQuick.Controls . В приведенном ниже коде показан файл main.qml , в котором создается само окно и задаются его размер и заголовок по умолчанию.
```c++
import QtQuick
import QtQuick.Controls
import Qt.labs.platform

ApplicationWindow {
	visible: true
	
	width: 640
	height: 480
	
	// ...
}
```
Окно `ApplicationWindow` состоит из четырех основных областей, как показано ниже. Строка меню, строка инструментов и строка состояния обычно заполняются экземплярами  элементов управления `MenuBar`, `ToolBar` или `TabBar`, а область содержимого представляет собой куда помещаются дочерние элементы окна. Обратите внимание, что в приложении просмотра изображений нет строки состояния, поэтому она отсутствует как в приведенном здесь коде, так и на рисунке выше.

![[QML Windows App.png]]

Поскольку мы ориентируемся на настольные компьютеры, мы принудительно используем стиль `Fusion`. Это можно сделать с помощью конфигурационного файла, переменных окружения, аргументов командной строки или программно в коде Си++. В последнем случае мы сделаем это, добавив в файл `main.cpp` следующую строку :
```c++
QQuickStyle::setStyle("Fusion");
```
Затем мы начинаем строить пользовательский интерфейс в файле main.qml, добавляя в качестве содержимого элемент `Image`. Этот элемент будет содержать изображения, когда пользователь откроет их, поэтому пока он является просто заполнителем. Свойство `background` используется для того, чтобы предоставить окну элемент, который будет располагаться за содержимым. Он будет отображаться, когда изображение не загружено, а также в виде границ вокруг изображения, если соотношение сторон не позволяет ему заполнить область содержимого окна.
```c++
ApplicationWindow {
	// ...
	background: Rectangle {
		color: "darkGray"
	}
	
	Image {
		id: image
		anchors.fill: parent
		fillMode: Image.PreserveAspectFit
		asynchronous: true
	}
	
	// ...
}
```

Далее мы добавляем панель инструментов . Для этого используется свойство `ToolBar` окна.  Внутри панели инструментов мы добавляем элемент `Flow`, который позволит содержимому заполнить всю ширину элемента управления, прежде чем оно перельется в новую строку. Внутри потока разместим кнопку `ToolButton`.
Кнопка `ToolButton` имеет несколько интересных свойств. С текстом все понятно. Однако имя `icon.name` взято из документа `freedesktop.org Icon Naming Specification` (https://specifications.freedesktop.org/icon-naming-spec/icon-naming-spec-latest.html) . В этом документе список
стандартных иконок перечисляется по именам. Обратившись к такому имени, Qt выберет нужную иконку из текущей темы рабочего стола.
В обработчике сигнала `onClicked` кнопки `ToolButton` находится последний фрагмент кода. Он вызывает метод `open` на элементе `fileOpenDialog`.
```c++
ApplicationWindow {
	// ...
	
	header: ToolBar {
		Flow {
			anchors.fill: parent
			
			ToolButton {
				text: qsTr("Open")
				icon.name: "document-open"
				onClicked: fileOpenDialog.open()
			}
		}
	}
	
// ...
}
```
Элемент `fileOpenDialog` представляет собой элемент управления `FileDialog` из модуля `Qt.labs.platform`. Файловый диалог может использоваться для открытия или сохранения файлов.
В коде мы начинаем с присвоения заголовка . Затем с помощью класса` StandardsPaths` задаем начальную папку. Класс `StandardsPaths` содержит ссылки на общие папки, такие как домашняя, документы и т.д. После этого мы задаем фильтр имен, который определяет, какие
файлы пользователь может видеть и выбирать с помощью диалога.
Наконец, мы доходим до обработчика сигнала `onAccepted`, где элемент` Image`, содержащий содержимое окна, устанавливается на отображение выбранного файла. Имеется также сигнал `onRejected`, но в приложении просмотра изображений его обрабатывать не
нужно.
```c++
ApplicationWindow {
	// ...
	
	FileDialog {
		id: fileOpenDialog
		title: "Select an image file"
		folder: StandardPaths.writableLocation(StandardPaths.DonameFilters: [
			"Image files (*.png *.jpeg *.jpg)",
		]

		onAccepted: {
			image.source = fileOpenDialog.fileUrl
		}
	}
	
	// ...
}
```
Далее мы переходим к строке меню `MenuBar` . Для создания меню внутри строки меню размещаются элементы `Menu`, а затем каждое `Menu` заполняется элементами `MenuItem`.
В приведенном ниже коде мы создаем два меню - `Файл` и `Справка`. В разделе `File` мы размещаем `Open`, используя ту же пиктограмму и действие, что и кнопка инструмента на панели инструментов. В разделе `Help` находится пункт `About`, который инициирует вызов
метода open элемента `aboutDialog`.
Обратите внимание, что амперсанд ("&") в свойстве `title Menu` и свойстве `text MenuItem` превращает следующий символ в комбинацию клавиш; например, вы попадаете в меню файла, нажав `Alt+F`, а затем `Alt+O` для вызова элемента `open`.
```c++
ApplicationWindow {
	// ...
	
	menuBar: MenuBar {
		Menu {
			title: qsTr("&File")
			
			MenuItem {
				text: qsTr("&Open...")
				icon.name: "document-open"
				onTriggered: fileOpenDialog.open()
			}
		}
		
		Menu {
			title: qsTr("&Help")
			
			MenuItem {
				text: qsTr("&About...")
				onTriggered: aboutDialog.open()
			}
		}
	}
	
// ...
}			
```
Элемент `aboutDialog` основан на элементе `Dialog` из модуля `QtQuick.Controls`, который является базой для пользовательских диалогов. Диалог, который мы собираемся создать, показан на рисунке ниже.
![[Qml ImageViewer_1.png]]

Код для `aboutDialog` можно разделить на три части. Сначала мы задаем диалоговому окну заголовок. Затем мы предоставляем содержимое диалога - в данном случае элемент управления `Label` . Наконец, для закрытия диалога мы используем стандартную кнопку
`Ok`.
```c++
ApplicationWindow {
	// ...
	
	Dialog {
		id: aboutDialog
		title: qsTr("About")
		
		Label {
			anchors.fill: parent
			text: qsTr("QML Image Viewer\nA part of the QmlBook")
			horizontalAlignment: Text.AlignHCenter
		}
		
		standardButtons: StandardButton.Ok
	}
	
	// ...
}
```

В итоге получилось функциональное, хотя и простое, настольное приложение для просмотра изображений.

## Пример просмотрщика изображений
```c++
import QtQuick 2.14
import Qt.labs.platform 1.0   //должен быть перед  QtQuick.Controls
import QtQuick.Window 2.14
import QtQuick.Controls 2.12
import Qt.labs.settings 1.1

ApplicationWindow {
    visible: true

    width: 640
    height: 480

    background: Rectangle {
        color: "darkGray"
    }

    Image {
        id: image
        anchors.fill: parent
        fillMode: Image.PreserveAspectFit
        asynchronous: true
    }

    header: ToolBar {
        Flow {
            anchors.fill: parent

            ToolButton {
                text: qsTr("Open")
                icon.name: "document-open"
                onClicked: fileOpenDialog.open()
            }
        }
    }

    FileDialog {
        id: fileOpenDialog
        title: "Select an image file"
        folder: StandardPaths.writableLocation(StandardPaths)

        nameFilters: ["Image files (*.png *.jpeg *.jpg)", ]

        onAccepted: {
            image.source = fileOpenDialog.fileUrl
        }
    }

    menuBar: MenuBar {
        Menu {
            title: qsTr("&File")

            MenuItem {
                text: qsTr("&Open...")
                icon.name: "document-open"
                onTriggered: fileOpenDialog.open()
            }
        }

        Menu {
            title: qsTr("&Help")

            MenuItem {
                text: qsTr("&About...")
                onTriggered: aboutDialog.open()
            }
        }
    }

    Dialog {
        id: aboutDialog
        title: qsTr("About")

        Label {
            anchors.fill: parent
            text: qsTr("QML Image Viewer\nA part of the QmlBook")
            horizontalAlignment: Text.AlignHCenter
        }

        anchors.centerIn: parent

        standardButtons: StandardButton.Ok
    }
}
```

## Переход на мобильную связь
Существует ряд отличий в том, как должен выглядеть и вести себя пользовательский интерфейс на мобильном устройстве по сравнению с настольным приложением. Самое большое отличие для нашего приложения - это способ доступа к действиям. Вместо панели меню и панели инструментов мы используем ящик, из которого пользователь может выбирать действия. Ящик можно пролистывать сбоку, но мы также предлагаем кнопку "гамбургер" в заголовке. Полученное приложение с открытым ящиком можно увидеть ниже.
![[Qml ImageViewer_2.png]]

Прежде всего, необходимо изменить стиль, заданный в файле `main.cpp`,:
```c++
QQuickStyle::setStyle("Material");
```

Затем мы приступаем к адаптации пользовательского интерфейса. Начнем с замены меню на ящик. В приведенном ниже коде компонент `Drawer` добавляется как дочернего окна `ApplicationWindow`. Внутри ящика мы разместили `ListView`, содержащий экземпляры `ItemDelegate`. Он также содержит индикатор `ScrollIndicator`, используемый для отображения части длинного списка. Поскольку наш список состоит только из двух
элементов, в данном примере индикатор не виден. 
`ListView` ящика заполняется из `ListModel`, где каждый `ListItem` соответствует пункту меню. При каждом щелчке на пункте меню в методе `onClicked` вызывается метод-триггер соответствующего `ListItem`. Таким образом, мы можем использовать один делегат
для запуска различных действий.

```c++
ApplicationWindow {
	// ...
	id: window
	
	Drawer {
		id: drawer
		width: Math.min(window.width, window.height) / 3 * 2
		height: window.height
		
		ListView {
			focus: true
			currentIndex: -1
			anchors.fill: parent
			
			delegate: ItemDelegate {
				width: parent.width
				text: model.text
				highlighted: ListView.isCurrentItem
				
				onClicked: {
					drawer.close()
					model.triggered()
				}
			}
			
			model: ListModel {
				
				ListElement {
					text: qsTr("Open...")
					
					triggered: function() { fileOpenDialog.open(); }
				}
					
				ListElement {
					text: qsTr("About...")
					
					triggered: function() { aboutDialog.open(); }
				}
			}
				
			ScrollIndicator.vertical: ScrollIndicator { }
		}
	}
		
	// ...
}
```
Следующее изменение - в заголовке окна `ApplicationWindow`. Вместо панели инструментов в стиле рабочего стола мы добавляем кнопку для открытия ящика и ярлык для заголовка нашего приложения.
![[Qml ImageViewer_3.png]]
`ToolBar` содержит два дочерних элемента: `ToolButton` и `Label`. Кнопка `ToolButton` открывает ящик. Соответствующая кнопка закрытия вызов находится в делегате `ListView`. После выбора элемента ящик закрывается. Иконка, используемая для кнопки `ToolButton`, взята со страницы `Material Design Icons` (https://material.io/tools/icons/? style=baseline) .
```c++
ApplicationWindow {
	// ...
	
	header: ToolBar {
		
		ToolButton {
			id: menuButton
			
			anchors.left: parent.left
			anchors.verticalCenter: parent.verticalCenter
			
			icon.source: "images/baseline-menu-24px.svg"
			
			onClicked: drawer.open()
		}

		Label {
			anchors.centerIn: parent
			
			text: "Image Viewer"
			
			font.pixelSize: 20
			
			elide: Label.ElideRight
		}
	}
	
	// ...
}
```

Наконец, мы сделаем фон панели инструментов красивым - или, по крайней мере, оранжевым. Для этого мы изменим свойство `Material.background attached`. Оно берется из модуля `QtQuick.Controls.Material` и влияет только на стиль `Material`.
```c++
import QtQuick.Controls.Material

ApplicationWindow {
	// ...
		
	header: ToolBar {
		Material.background: Material.Orange
	}	
	
	// ...
```
С помощью этих небольших изменений мы преобразовали наш просмотрщик изображений для настольных компьютеров в версию, удобную для мобильных устройств.

## Общая кодовая база
В предыдущих двух разделах мы рассмотрели программу просмотра изображений, разработанную для настольных компьютеров, а затем адаптировали ее для мобильных устройств.
Если посмотреть на кодовую базу, то большая часть кода по-прежнему является общей. Те части, которые разделяются, в основном связаны с документом приложения, т.е. с изображением. Изменения учитывают различные паттерны взаимодействия на настольных и мобильных компьютерах. Естественно, мы хотим унифицировать эти кодовые базы. **QML** поддерживает это с помощью селекторов файлов.
Селектор файлов позволяет заменять отдельные файлы в зависимости от того, какие селекторы активны. В документации по Qt список селекторов содержится в документации по классу `QFileSelector` (ссылка (https://doc.qt.io/qt-5/qfileselector.html) ). В нашем случае мы сделаем настольную версию по умолчанию и будем заменять выбранные файлы при появлении селектора `android`. В процессе разработки можно установить переменную окружения `QT_FILE_SELECTORS` в значение `android` для имитации этого.

> Селекторы файлов работают, заменяя файлы на альтернативные, когда присутствует селектор.
> Создав каталог с именем `+selector` (где `selector` - имя селектора) в том же каталоге, что и заменяемые файлы, вы можете поместить в него файлы с тем же именем, что и  заменяемый файл. При наличии селектора файл в каталоге будет выбран вместо исходного файла. 
> Селекторы основаны на платформе: например, `android`, `ios`, `osx`, `linux`, `qnx` и т.д. Они также могут включать название используемого дистрибутива `Linux` (если он идентифицирован), например `debian`, `ubuntu`, `fedora`. Наконец, в них также указывается локаль, например `en_US`, `sv_SE` и т.д.
> Также возможно добавление собственных пользовательских селекторов.

Первым шагом для выполнения этого изменения является изоляция общего кода. Для этого мы создаем элемент `ImageViewerWindow`, который будет использоваться вместо  `ApplicationWindow` для обоих вариантов. Он будет состоять из диалоговых окон, элемента `Image` и фона. Для того чтобы сделать методы открытия диалогов доступными для кода, специфичного для платформы, необходимо раскрыть их через функции `openFileDialog` и `openAboutDialog`.
```c++
import QtQuick
import QtQuick.Controls
import Qt.labs.platform

ApplicationWindow {
	function openFileDialog() { fileOpenDialog.open(); }
	function openAboutDialog() { aboutDialog.open(); }

	visible: true
	title: qsTr("Image Viewer")
	
	background: Rectangle {
		color: "darkGray"
	}
	
	Image {
		id: image
		anchors.fill: parent
		fillMode: Image.PreserveAspectFit
		asynchronous: true
	}
	
	FileDialog {
		id: fileOpenDialog
		
		// ...
	}
	
	Dialog {
		id: aboutDialog
		
		// ...
	}
}
```
Далее мы создаем новый файл `main.qml` для нашего стандартного стиля `Fusion`, т.е. для настольной версии пользовательского интерфейса.
Здесь мы основываем пользовательский интерфейс на окне `ImageViewerWindow` вместо `ApplicationWindow`. Затем мы добавляем к нему специфические для данной платформы элементы, например, `MenuBar` и `ToolBar`. Единственным изменением в них является то, что вызовы для открытия соответствующих диалогов осуществляются не непосредственно к элементам управления диалогами, а к новым функциям.
```c++
import QtQuick
import QtQuick.Controls

ImageViewerWindow {
	id: window
	width: 640
	height: 480
	
	menuBar: MenuBar {
		
		Menu {
			title: qsTr("&File")
			
			MenuItem {
				text: qsTr("&Open...")
				icon.name: "document-open"
				onTriggered: window.openFileDialog()
			}
		}
		
		Menu {
			title: qsTr("&Help")
			
			MenuItem {
				text: qsTr("&About...")
				onTriggered: window.openAboutDialog()
			}
		}
	}
	
	header: ToolBar {
		
		Flow {
			anchors.fill: parent
			
			ToolButton {
				text: qsTr("Open")
				icon.name: "document-open"
				onClicked: window.openFileDialog()
			}
		}
	}
}
```
Далее необходимо создать специфический для мобильных устройств файл `main.qml`. Он будет основан на теме `Material`.
Здесь мы сохраняем `Drawer` и панель инструментов для мобильных устройств. Единственное изменение - это способ открытия диалогов.
```c++
import QtQuick
import QtQuick.Controls
import QtQuick.Controls.Material

ImageViewerWindow {
	id: window
	width: 360
	height: 520
	
	Drawer {
		id: drawer
		// ...
		
		ListView {
			// ...
			
			model: ListModel {
				
				ListElement {
					text: qsTr("Open...")
					triggered: function(){ window.openFileDialog()
				}
				
			ListElement {
				text: qsTr("About...")
				triggered: function(){window.openAboutDialog()
				}
			}
			
			// ...
		}
	}

	header: ToolBar {
	// ...
	}
}
```
Два файла `main.qml` размещаются в файловой системе, как показано ниже. Это позволяет селектору файлов, который автоматически создается движком `QML`, выбрать нужный файл. По умолчанию загружается файл Fusion `main.qml`. Если присутствует селектор `android`, то вместо него загружается Material `main.qml`.
![[QML Viewer.png]]
До сих пор стиль задавался в файле `main.cpp`. Мы могли бы продолжать в том же духе и использовать выражения \#ifdef для установки различных стилей для разных платформ. Вместо этого мы снова воспользуемся механизмом выбора файлов и зададим стиль с
помощью конфигурационного файла. Ниже показан файл для стиля `Material`, но файл `Fusion` не менее прост.
```c++
[Controls]
Style=Material
```
В результате этих изменений мы получили объединенную кодовую базу, в которой весь код документа является общим, а различаются только шаблоны взаимодействия с  пользователем. Это можно сделать разными способами, например, храня документ в отдельном компоненте, который включается в интерфейсы для каждой платформы, или, как в данном примере, создавая общую базу, которая расширяется для каждой платформы. 
Оптимальный подход лучше всего определить, когда вы знаете, как выглядит ваша  конкретная кодовая база, и можете решить, как отделить общее от уникального.

## Родные диалоги
При работе с программой просмотра изображений можно заметить, что в ней используется нестандартный диалог выбора файлов. Из-за этого он выглядит нестандартно. В решении этой проблемы нам может помочь модуль `Qt.labs.platform`. Он обеспечивает привязку QML к собственным диалоговым окнам, таким как диалог файла, диалог шрифта и диалог
цвета. Он также предоставляет API для создания значков в системном трее, а также глобальных меню системы, которые располагаются поверх экрана (например, как в OS X). Ценой этого является зависимость от модуля **QtWidgets** , поскольку диалог на основе виджетов используется в качестве запасного варианта при отсутствии встроенной поддержки.
Для того чтобы интегрировать в программу просмотра изображений собственный файловый диалог, нам необходимо импортировать модуль `Qt.labs.platform`. Поскольку этот модуль имеет несовпадение имен с модулем `QtQuick.Dialogs`, который он заменяет, важно удалить старый оператор import.
В самом элементе файлового диалога необходимо изменить способ установки свойства `folder` и убедиться, что обработчик `onAccepted` использует свойство `file`, а не свойство `fileUrl` . За исключением этих деталей, использование идентично `FileDialog` из `QtQuick.Dialogs`.
```c++
import QtQuick
import QtQuick.Controls
import Qt.labs.platform

ApplicationWindow {
	// ...
	
	FileDialog {
		id: fileOpenDialog
		title: "Select an image file"
		folder: StandardPaths.writableLocation(StandardPaths.Do())
		nameFilters: [
			"Image files (*.png *.jpeg *.jpg)",
		]
	
		onAccepted: {
			image.source = fileOpenDialog.file
		}
	}
		
	// ...
}
```
Помимо изменений в QML, нам также необходимо изменить файл проекта программы просмотра изображений, чтобы включить в него модуль виджетов.
```c++
QT += quick quickcontrols2 widgets
```
И нам необходимо обновить файл `main.qml`, чтобы инстанцировать объект `QApplication` вместо объекта `QGuiApplication`. Это связано с тем, что класс `QGuiApplication` содержит минимальную среду, необходимую для работы графического приложения, а `QApplication`
расширяет `QGuiApplication` функциями, необходимыми для поддержки `QtWidgets`.
```c++
include <QApplication>

// ...
int main(int argc, char *argv[])
{
	QApplication app(argc, argv);
	// ...
}	
```
Благодаря этим изменениям программа просмотра изображений теперь будет использовать собственные диалоговые окна на большинстве платформ. Поддерживаются следующие платформы: iOS, Linux (с платформенной темой GTK+), macOS, Windows и WinRT. Для Android
будет использоваться стандартный диалог `Qt`, предоставляемый модулем `QtWidgets`.



































