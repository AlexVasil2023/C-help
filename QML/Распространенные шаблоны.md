# Распространенные шаблоны
Существует ряд общих шаблонов пользовательского интерфейса, которые могут быть реализованы с помощью `Qt Quick Controls`. В этом разделе мы попытаемся продемонстрировать, как можно построить некоторые из наиболее распространенных шаблонов.

## Вложенные экраны
Для данного примера мы создадим дерево страниц, на которые можно попасть с предыдущего уровня экранов. Структура изображена ниже.
![[QML Screen.png]]

Ключевым компонентом в этом типе пользовательского интерфейса является `StackView`. Он позволяет размещать страницы в стеке, который затем может быть извлечен, когда пользователь захочет вернуться назад. В приведенном здесь примере мы покажем, как это можно реализовать. 
Начальный главный экран приложения показан на рисунке ниже.
![[QML Screen_1.png]]
Приложение начинается в файле `main.qml`, где у нас есть `ApplicationWindow`, содержащее `ToolBar`, `Drawer`, `StackView` и элемент домашней страницы `Home`. Ниже мы рассмотрим каждый из этих компонентов.
```c++
import QtQuick
import QtQuick.Controls

ApplicationWindow {
	// ...

	header: ToolBar {
		// ...
	}

	Drawer {
		// ...
	}
	
	StackView {
		id: stackView
		anchors.fill: parent
		initialItem: Home {}
	}
}
```
Домашняя страница `Home.qml` состоит из `Page`, который является n-элементом управления, поддерживающим верхние и нижние колонтитулы. В этом примере мы просто центрируем на странице `Label` с текстом `Home Screen`. Это работает потому, что содержимое `StackView` автоматически заполняет представление стека, поэтому страница будет иметь нужный размер для работы.
```c++
import QtQuick
import QtQuick.Controls

Page {
	title: qsTr("Home")
	
	Label {
		anchors.centerIn: parent
		text: qsTr("Home Screen")
	}
}
```
Возвращаясь к файлу `main.qml` , мы переходим к рассмотрению части, посвященной ящику. Именно с нее начинается навигация по страницам. Активными частями пользовательского интерфейса являются элементы `ItemDelegate`. В обработчике `onClicked` очередная страница помещается в `stackView`.
Как показано в приведенном ниже коде, в стек можно поместить либо **Компонент**, либо ссылку на конкретный QML-файл. В любом случае создается новый экземпляр и помещается в стек.
```c++
ApplicationWindow {
	// ...
	
	Drawer {
		id: drawer
		width: window.width * 0.66
		height: window.height

		Column {
			anchors.fill: parent
		
			ItemDelegate {
				text: qsTr("Profile")
				width: parent.width
				
				onClicked: {
					stackView.push("Profile.qml")
					drawer.close()
				}
			}

			ItemDelegate {
				text: qsTr("About")
				width: parent.width

				onClicked: {
					stackView.push(aboutPage)
					drawer.close()
				}
			}
		}
	}

	// ...

	Component {
		id: aboutPage
		About {}
	}
	
	// ...
}
```
Вторая половина головоломки - панель инструментов. Идея заключается в том, что кнопка "Назад" отображается, если в `stackView` содержится более одной страницы, в противном случае отображается кнопка меню. Логику этого можно увидеть в свойстве `text`, где строки
`\\u...` представляют собой необходимые нам символы юникода.
В обработчике `onClicked` мы видим, что если в стеке находится более одной страницы, то стек разворачивается, т.е. удаляется верхняя страница. Если в стеке находится только один элемент, т.е. главный экран, то открывается ящик. 
Ниже панели инструментов расположен элемент `Label`. Этот элемент отображает название каждой страницы в центре заголовка.
```c++
ApplicationWindow {
	// ...
	
	header: ToolBar {
		contentHeight: toolButton.implicitHeight
		
		ToolButton {
			id: toolButton
			text: stackView.depth > 1 ? "\u25C0" : "\u2630"
			font.pixelSize: Qt.application.font.pixelSize * 1.6
			
			onClicked: {
				if (stackView.depth > 1) {
					stackView.pop()
				} else {
					drawer.open()
				}
			}
		}
	
		Label {
			text: stackView.currentItem.title
			anchors.centerIn: parent
		}
	}

	// ...
}
```
Теперь мы рассмотрели, как попасть на страницы `About` и `Profile`, но мы также хотим сделать так, чтобы со страницы `Profile` можно было попасть на страницу `Edit Profile`. Это делается с помощью кнопки на странице профиля. При нажатии на кнопку файл `EditProfile.qml` помещается в `StackView`.
![[QML_Templ.png]]
```c++
import QtQuick
import QtQuick.Controls

Page {
	title: qsTr("Profile")
	
	Column {
		anchors.centerIn: parent
		spacing: 10
		
		Label {
			anchors.horizontalCenter: parent.horizontalCenter
			text: qsTr("Profile")
		}
		
		Button {
			anchors.horizontalCenter: parent.horizontalCenter
			text: qsTr("Edit");
			
			onClicked: stackView.push("EditProfile.qml")
		}
	}
}
```
## Экраны "бок о бок"
Для данного примера мы создаем пользовательский интерфейс, состоящий из трех страниц, по которым пользователь может перемещаться. Страницы показаны на диаграмме ниже. Это может быть интерфейс приложения для отслеживания состояния здоровья, отслеживающего текущее состояние, статистику пользователя и общую статистику.
![[QML Templ_2.png]]
На рисунке ниже показано, как выглядит страница `Current` в приложении. Основная часть экрана управляется с помощью `SwipeView`, что позволяет реализовать схему взаимодействия с экраном "бок о бок". Заголовок и текст, показанные на рисунке, взяты из страницы внутри `SwipeView`, а `PageIndicator` (три точки внизу) - из файла `main.qml` и располагается под `SwipeView`. Индикатор страницы показывает пользователю, какая страница активна в данный момент, что помогает при навигации.
![[QML Emul_3.png]]
Если заглянуть в файл `main.qml`, то он состоит из окна `ApplicationWindow` с файлом
```c++
//SwipeView.

import QtQuick
import QtQuick.Controls

ApplicationWindow {
	visible: true
	width: 640
	height: 480
	title: qsTr("Side-by-side")
	
	SwipeView {
	// ...
	}
	
	// ...
}
```
Внутри `SwipeView` каждая из дочерних страниц инстанцируется в том порядке, в котором они должны отображаться. Это `Current`, `UserStats` и `TotalStats` 
```c++
ApplicationWindow {
	// ...
	
	SwipeView {
		id: swipeView
		anchors.fill: parent
		
		Current {
		}

		UserStats {
		}

		TotalStats {
		}
	}
	
	// ...
}
```
Наконец, свойства `count` и `currentIndex SwipeView` привязываются к элементу `PageIndicator`. На этом структура вокруг страниц завершена.
```c++
ApplicationWindow {
	// ...
	
	SwipeView {
		id: swipeView
		// ...
	}
	
	PageIndicator {
		anchors.bottom: parent.bottom
		anchors.horizontalCenter: parent.horizontalCenter
		currentIndex: swipeView.currentIndex
		count: swipeView.count
	}
}
```
Каждая страница состоит из `Page` с заголовком, состоящим из `Label` и некоторого содержимого. Для страниц `Current` и `User Stats` содержимое состоит из простого `Label`, а для страницы `Community Stats` предусмотрена кнопка "Назад".
```c++
import QtQuick
import QtQuick.Controls

Page {
	
	header: Label {
		text: qsTr("Community Stats")
		font.pixelSize: Qt.application.font.pixelSize * 2
		padding: 10
	}
	
	// ...
}
```
![[QML Templ_3.png]]
Кнопка "Назад" явно вызывает функцию `setCurrentIndex` окна `SwipeView` чтобы установить индекс в ноль, возвращая пользователя непосредственно на текущую страницу. При каждом переходе между страницами `SwipeView` обеспечивает переход, поэтому даже при явном изменении индекса пользователь получает представление о направлении движения.

> При программной навигации в `SwipeView` важно не устанавливать `currentIndex` путем присваивания в `JavaScript`. Это связано с тем, что в этом случае будут нарушены все привязки QML, которые он переопределяет. Вместо этого используйте методы `setCurrentIndex` , `incrementCurrentIndex` и `decrementCurrentIndex` . Это позволит сохранить привязки QML.

```c++
Page {
	// ...
	
	Column {
		anchors.centerIn: parent
		spacing: 10
		
		Label {
			anchors.horizontalCenter: parent.horizontalCenter
			text: qsTr("Community statistics")
		}
		
		Button {
			anchors.horizontalCenter: parent.horizontalCenter
			text: qsTr("Back")
			
			onClicked: swipeView.setCurrentIndex(0);
		}
	}
}
```
## Окна документов
В данном примере показано, как реализовать ориентированный на рабочий стол пользовательский интерфейс, ориентированный на работу с документами. Идея заключается в том, чтобы иметь одно окно для каждого документа. При открытии нового документа открывается новое окно. Для пользователя каждое окно - это самодостаточный мир с единственным документом.
![[QML Templ_4.png]]
Код начинается с окна `ApplicationWindow` с меню `File` со стандартными операциями: `New`, `Open`, `Save` и `Save As`. Мы помещаем это в файл `DocumentWindow.qml`.
Мы импортировали `Qt.labs.platform` для нативных диалогов и внесли последующие изменения в файл проекта и файл `main.cpp`, как описано в разделе о нативных диалогах выше.
```c++
import QtQuick
import QtQuick.Controls
import Qt.labs.platform as NativeDialogs

ApplicationWindow {
	id: root
	
	// ...

	menuBar: MenuBar {
		
		Menu {
			title: qsTr("&File")
			
			MenuItem {
				text: qsTr("&New")
				icon.name: "document-new"
				
				onTriggered: root.newDocument()
			}
			
			MenuSeparator {}
			
			MenuItem {
				text: qsTr("&Open")
				icon.name: "document-open"
				
				onTriggered: openDocument()
			}
			
			MenuItem {
				text: qsTr("&Save")
				icon.name: "document-save"
				
				onTriggered: saveDocument()
			}
		
			MenuItem {
				text: qsTr("Save &As...")
				icon.name: "document-save-as"
				
				onTriggered: saveAsDocument()
			}
		}
	}

	// ...
}
```

Для загрузки программы мы создаем первый экземпляр `DocumentWindow` из файла `main.qml` , который является точкой входа в приложение.
```c++
import QtQuick
	DocumentWindow {
	visible: true
}
```
В примере, приведенном в начале этой главы, каждый `MenuItem` при срабатывании вызывает соответствующую функцию. Начнем с элемента `New`, который вызывает функцию `newDocument`. Эта функция, в свою очередь, опирается на функцию `createNewDocument`, которая динамически создает из файла `DocumentWindow.qml` новый экземпляр элемента, т.е. новый экземпляр `DocumentWindow`. Причина выделения этой части новой функции заключается в том, что мы используем ее и при открытии документов.
Обратите внимание, что мы не указываем родительский элемент при создании нового экземпляра с помощью `createObject` . Таким образом, мы создаем новые элементы верхнего уровня. Если бы мы указали текущий документ в качестве родительского для следующего, то разрушение родительского окна привело бы к разрушению дочерних окон.
```c++
ApplicationWindow {
	
	// ...
	
	function createNewDocument()
	{
		var component = Qt.createComponent("DocumentWindow.qml")
		var window = component.createObject();
		return window;
	}
		
	function newDocument()
	{
		var window = createNewDocument();
		window.show();
	}
		
	// ...
}
```

Если посмотреть на элемент `Open`, то мы увидим, что он вызывает функцию `openDocument`. Эта функция просто открывает диалог `openDialog`, который позволяет пользователю выбрать файл для открытия. Поскольку у нас нет формата документа, расширения файла
или чего-либо подобного, большинство свойств диалога установлены в значения по умолчанию. В реальном приложении это было бы лучше настроить.
В обработчике `onAccepted` с помощью метода `createNewDocument` инстанцируется окно нового документа, а перед его отображением задается имя файла. В этом случае реальной загрузки не происходит.

> Мы импортировали модуль `Qt.labs.platform` как NativeDialogs. Это связано с тем, что он предоставляет `MenuItem` , который конфликтует с `MenuItem`, предоставляемым модулем `QtQuick.Controls`.

```c++
ApplicationWindow {
	// ...
	
	function openDocument(fileName)	{
		openDialog.open();
	}

	NativeDialogs.FileDialog {
		id: openDialog
		title: "Open"
		folder: NativeDialogs.StandardPaths.writableLocation(Na)
		
		onAccepted: {
			var window = root.createNewDocument();
			window.fileName = openDialog.file;
			window.show();
		}
	}
// ...
}
```
Имени файла принадлежит пара свойств, описывающих документ: `fileName` и `isDirty`. В `fileName` хранится имя файла документа, а `isDirty` устанавливается, если в документе есть несохраненные изменения. Это используется логикой сохранения и сохранения как,
которая показана ниже. 
При попытке сохранить документ без имени вызывается `saveAsDocument`. В результате происходит обход диалогового окна `saveAsDialog`, которое задает имя файла, а затем в обработчике `onAccepted` снова пытается сохранить. 
Обратите внимание, что функции `saveAsDocument` и `saveDocument` соответствуют пунктам меню `Save As` и `Save`.
После сохранения документа в функции `saveDocument` проверяется свойство `tryToClose`. Этот флаг устанавливается, если сохранение происходит в результате того, что пользователь хочет сохранить документ в момент закрытия окна. Как следствие, после выполнения операции сохранения окно будет закрыто. Опять же, в данном примере никакого сохранения не происходит.
```c++
ApplicationWindow {
	// ...

	property bool isDirty: true // Has the document got
	property string fileName // The filename of the d
	property bool tryingToClose: false // Is the window trying
	
	// ...
	function saveAsDocument()
	{
		saveAsDialog.open();
	}
	
	function saveDocument()
	{
		if (fileName.length === 0)
		{
			root.saveAsDocument();
		}
		else
		{
			// Save document here
			console.log("Saving document")
			root.isDirty = false;
			
			if (root.tryingToClose)
				root.close();
		}
	}
	
	NativeDialogs.FileDialog {
		id: saveAsDialog
		title: "Save As"
		folder: NativeDialogs.StandardPaths.writableLocation(Na)
		
		onAccepted: {
			root.fileName = saveAsDialog.file
			saveDocument();
		}
		
		onRejected: {
			root.tryingToClose = false;
		}
	}
	
	// ...
}
```
Это приводит нас к закрытию окон. При закрытии окна вызывается обработчик `onClosing`. Здесь код может выбрать, не принимать ли запрос на закрытие. Если в документе есть несохраненные изменения, то мы открываем диалог `closeWarningDialog` и отклоняем
запрос на закрытие. 
Диалог `closeWarningDialog` спрашивает пользователя, следует ли сохранить изменения, но у пользователя также есть возможность отменить операцию закрытия. Отмена, обрабатываемая в `onRejected`, является самым простым случаем, поскольку мы отклонили закрытие при открытии диалога.
Если пользователь не хочет сохранять изменения, то есть в `onNoClicked`, флаг `isDirty` устанавливается в `false` и окно снова закрывается. На этот раз `onClosing` примет закрытие, так как `isDirty` равен `false`.
Наконец, когда пользователь хочет сохранить изменения, мы устанавливаем флаг `tryToClose` в `true` перед вызовом `save`. Это приводит нас к логике сохранения/сохранения как.
```c++
ApplicationWindow {
	
	// ...
	
	onClosing: {
		if (root.isDirty) {
			closeWarningDialog.open();
			close.accepted = false;
		}
	}
	
	NativeDialogs.MessageDialog {
		id: closeWarningDialog
		title: "Closing document"
		text: "You have unsaved changed. Do you want to save yo
		buttons: NativeDialogs.MessageDialog.Yes | NativeDialog
		
		onYesClicked: {
			// Attempt to save the document
			root.tryingToClose = true;
			root.saveDocument();
		}
		
		onNoClicked: {
			// Close the window
			root.isDirty = false;
			root.close()
		}

		onRejected: {
			// Do nothing, aborting the closing of the window
		}
	}
}
```
Ниже показан весь поток для логики закрытия и сохранения/сохранения в виде. Вход в систему осуществляется в состоянии "закрыто", а состояния "закрыто" и "не закрыто" являются исходами.
Это выглядит сложным по сравнению с реализацией этого с помощью `Qt Widgets` и `C++`. Это связано с тем, что диалоги не блокируются в QML. Это означает, что мы не можем ожидать результата диалога в операторе `switch`.
Вместо этого нам необходимо запомнить состояние и продолжить выполнение операции в соответствующих обработчиках `onYesClicked`, `onNoClicked`, `onAccepted` и `onRejected`.
![[QML Templ_5.png]]
Последний фрагмент головоломки - заголовок окна. Он состоит из свойства `fileName` и `isDirty`.
```c++
ApplicationWindow {
	
	// ...
	
	title: (fileName.length===0 ? qsTr("Document"):fileName) + (i)
	
	// ...
}
```
Этот пример далеко не полный. Например, документ не загружается и не сохраняется. Еще один недостающий элемент - обработка случая закрытия всех окон одним махом, т.е. выхода из приложения. Для этой функции необходим синглтон, хранящий список всех текущих экземпляров DocumentWindow . Однако это будет лишь еще один способ инициировать закрытие окна, поэтому приведенная здесь логическая схема остается актуальной.














