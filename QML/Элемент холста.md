#QML_canvas
# Элемент холста

Одним из достоинств QML является его близость к экосистеме Javascript. Это позволяет нам повторно использовать существующие решения из веб-пространства и сочетать их с "родной" производительностью визуальных средств QML. Однако иногда хочется повторно использовать и графические решения из веб-пространства. 

Здесь на помощь приходит элемент [[Canvas|Canvas]]. Элемент [[Canvas|Canvas]] предоставляет API, очень близкий к API рисования для одноименного элемента HTML.
![[QML Canvas.png]]
Основная идея элемента [[Canvas|canvas]] заключается в визуализации контуров с помощью контекстного 2D-объекта. Контекстный 2D-объект содержит необходимые графические функции, в то время как холст выступает в качестве канвы для рисования. Контекст 2D поддерживает обводки, градиенты заливки, текст и различный набор команд для создания контуров.

Рассмотрим пример рисования простой траектории:
```c++
import QtQuick

Canvas {
	id: root
	
	// canvas size
	width: 200; height: 200
	
	// handler to override for drawing
	onPaint: {
		// get context to draw with
		var ctx = getContext("2d")
		
		// setup the stroke
		ctx.lineWidth = 4
		ctx.strokeStyle = "blue"
		
		// setup the fill
		ctx.fillStyle = "steelblue"
		
		// begin a new path to draw
		ctx.beginPath()
		
		// top-left start point
		ctx.moveTo(50,50)
		
		// upper line
		ctx.lineTo(150,50)
		
		// right line
		ctx.lineTo(150,150)
		
		// bottom line
		ctx.lineTo(50,150)
		
		// left line through path closing
		ctx.closePath()
		
		// fill using fill style
		ctx.fill()
		
		// stroke using line width and stroke style
		ctx.stroke()
	}
}
```
В результате образуется заполненный прямоугольник с начальной точкой 50,50 и размером 100 и обводкой, используемой в качестве украшения границы.
![[QML canvas_1.png]]
Ширина обводки установлена на 4 и использует синий цвет, определяемый [[strokeStyle|strokeStyle]]. Конечная фигура заливается через `fillStyle` цветом "стальной синий". Только при вызове функций `stroke` или `fill` будет нарисован реальный контур, и они могут использоваться независимо друг от друга. При вызове функции `stroke` или `fill` будет нарисован текущий контур. Сохранить контур для последующего использования невозможно, можно только сохранить и восстановить состояние рисования.

В QML элемент `Canvas` выступает в качестве контейнера для рисунка. Объект 2D-контекста обеспечивает фактическое выполнение операции рисования. Собственно рисование должно выполняться внутри обработчика события `onPaint`
```c++
Canvas {
	width: 200; height: 200
	
	onPaint: {
		var ctx = getContext("2d")
		// setup your path
		// fill or/and stroke
	}
}
```
Сам холст представляет собой типичную двумерную декартову систему координат, в которой левый верхний угол является точкой (0,0). Большее значение `y` идет вниз, а большое значение `x` - вправо.

Типичный порядок команд для этого API, основанного на пути, следующий:
>1. Настройка обводки и/или заливки
>2. Создать путь
>3. Штрих и/или заливка

```c++
onPaint: {
	var ctx = getContext("2d")
	
	// setup the stroke
	ctx.strokeStyle = "red"
	
	// create a path
	ctx.beginPath()
	ctx.moveTo(50,50)
	ctx.lineTo(150,50)
	
	// stroke path
	ctx.stroke()
}
```
Получается горизонтальная штриховая линия от точки `P1(50,50)` до точки
`P2(150,50)`.
![[QML canvas_2.png]]

> Как правило, при сбросе пути всегда требуется задать начальную точку, поэтому первой операцией после `beginPath` часто является `moveTo`.

## Удобный API
Для операций с прямоугольниками предусмотрен удобный API, который рисует напрямую и не требует вызова обводки или заливки.
```c++
import QtQuick
	
	Canvas {
		id: root
		width: 120; height: 120
		
		onPaint: {
			var ctx = getContext("2d")
			ctx.fillStyle = 'green'
			ctx.strokeStyle = "blue"
			ctx.lineWidth = 4
			
			// draw a filles rectangle
			ctx.fillRect(20, 20, 80, 80)
			// cut our an inner rectangle
			ctx.clearRect(30,30, 60, 60)
			// stroke a border from top-left to
			// inner center of the larger rectangle
			ctx.strokeRect(20,20, 40, 40)
		}
	}
```

![[QML canvas_3.png]]

> Область обводки занимает половину ширины линии по обе стороны от контура. При ширине линии `4px` будет нарисовано `2px` вне контура и `2px` внутри.

## Градиенты
Canvas может заливать фигуры не только цветом, но и градиентами или изображениями.
```c++
onPaint: {
	var ctx = getContext("2d")
	
	var gradient = ctx.createLinearGradient(100,0,100,200)
		gradient.addColorStop(0, "blue")
		gradient.addColorStop(0.5, "lightsteelblue")

		ctx.fillStyle = gradient
		ctx.fillRect(50,50,100,100)
}
```
В данном примере градиент задается вдоль начальной точки (100,0) до конечной (100,200), что дает вертикальную линию в центре холста. Стопы градиента могут быть заданы в виде цвета от 0,0 (начальная точка градиента) до 1,0 (конечная точка градиента). Здесь мы используем синий цвет в точке 0.0 (100,0) и светло-голубой в точке 0.5 (100,200). Градиент определен как значительно больший, чем прямоугольник, который мы хотим нарисовать, поэтому прямоугольник облегает градиент в соответствии с его геометрией.

![[QML canvas_4.png]]

> Градиент задается в координатах холста, а не в координатах относительно закрашиваемого контура. В холсте нет понятия относительных координат, к которому мы уже привыкли из QML.

## Тени
С помощью объекта 2D-контекста можно визуально улучшить контур с помощью теней. Тень - это область вокруг контура со смещением, цветом и заданным размытием. Для этого можно указать `shadowColor`, `shadowOffsetX`, `shadowOffsetY` и `shadowBlur`. Все это должно быть определено с помощью 2D-контекста. 2D-контекст является единственным API для операций рисования.

Тень также может быть использована для создания эффекта свечения вокруг контура. В следующем примере мы создаем текст "Canvas" с белым свечением вокруг. Все это на темном фоне для лучшей видимости.

Сначала мы рисуем темный фон:
```c++
// setup a dark background
ctx.strokeStyle = "#333"
ctx.fillRect(0,0,canvas.width,canvas.height);
```
затем мы определяем нашу теневую конфигурацию, которая будет использоваться для следующего пути:
```c++
// setup a blue shadow
ctx.shadowColor = "#2ed5fa";
ctx.shadowOffsetX = 2;
ctx.shadowOffsetY = 2;
ctx.shadowBlur = 10;
```
Наконец, мы нарисуем текст "Canvas", используя большой полужирный шрифт размером `80px` из файла.
Семейство шрифтов Ubuntu.
```c++
// render green text
ctx.font = 'bold 80px sans-serif';
ctx.fillStyle = "#24d12e";
ctx.fillText("Canvas!",30,180);
```
![[QML canvas_5.png]]

## Изображения

Канва QML поддерживает рисование изображений из нескольких источников. Чтобы использовать изображение внутри холста, его необходимо сначала загрузить. В приведенном ниже примере для загрузки изображения используется обработчик `Component.onCompleted`
```c++
onPaint: {
	var ctx = getContext("2d")

	// draw an image
	ctx.drawImage('assets/ball.png', 10, 10)
	
	// store current context setup
	ctx.save()
	ctx.strokeStyle = '#ff2a68'
	
	// create a triangle as clip region
	ctx.beginPath()
		ctx.moveTo(110,10)
		ctx.lineTo(155,10)
		ctx.lineTo(135,55)
	ctx.closePath()
	
	// translate coordinate system
	ctx.clip() // create clip from the path
	
	// draw image with clip applied
	ctx.drawImage('assets/ball.png', 100, 10)
	
	// draw stroke around path
	ctx.stroke()
	
	// restore previous context
	ctx.restore()
}
Component.onCompleted: {
	loadImage("assets/ball.png")
}
```
Слева показано изображение шара, нарисованное в левом верхнем углу в позиции 10x10. На правом изображении шар показан с применением обтравочного контура. Изображения и любые другие контуры могут быть обрезаны с помощью другого контура. Обрезка выполняется путем определения пути и вызова функции `clip()`. Теперь все последующие операции рисования будут обрезаться по этому пути. Отключить обрезку можно, восстановив предыдущее состояние или установив область обрезки на весь холст.

![[QML canvas_6.png]]

## Трансформация
Холст позволяет трансформировать систему координат несколькими способами. Это очень похоже на преобразования, предлагаемые элементами QML. Вы можете масштабировать , поворачивать , переводить систему координат.

В QML началом преобразования всегда является начало холста. Например, чтобы масштабировать контур вокруг его центра, необходимо перевести начало холста в центр контура. Также можно применить более сложное преобразование с помощью метода `transform`.
```c++
import QtQuick

Canvas {
	id: root
	width: 240; height: 120
	
	onPaint: {
		var ctx = getContext("2d")
		var ctx = getContext("2d");
		
		ctx.lineWidth = 4;
		ctx.strokeStyle = "blue";
		
		// translate x/y coordinate system
		ctx.translate(root.width/2, root.height/2);
		
		// draw path
		ctx.beginPath();
		ctx.rect(-20, -20, 40, 40);
		ctx.stroke();
		
		// rotate coordinate system
		ctx.rotate(Math.PI/4);
		ctx.strokeStyle = "green";

		// draw path
		ctx.beginPath();
		ctx.rect(-20, -20, 40, 40);
		ctx.stroke();
```
Кроме перевода холст позволяет также масштабировать с помощью `scale(x,y)` по осям `x` и `y`, поворачивать с помощью `rotate(angle)`, где угол задается радиусом (360 градусов = `2*Math.PI`), и использовать матричное преобразование с помощью `setTransform(m11, m12, m21, m22, dx, dy)`.

> Для сброса любого преобразования можно вызвать функцию `resetTransform()`
> 
> функция для установки матрицы преобразования обратно в матрицу тождества: ctx.resetTransform()

## Режимы композиции
Композиция позволяет нарисовать фигуру и смешать ее с существующими пикселями. Холст поддерживает несколько режимов композиции с помощью операций `globalCompositeOperation(mode)`. Например:
> - source-over
> 
> - source-in
>
> -source-out
>
> - source-atop

Начнем с небольшого примера, демонстрирующего эксклюзив или композицию:
```c++
onPaint: {
	var ctx = getContext("2d")
	ctx.globalCompositeOperation = "xor"
	ctx.fillStyle = "#33a9ff"
	
	for(var i=0; i<40; i++) {
		ctx.beginPath()
		ctx.arc(Math.random()*400, Math.random()*200, 20, 0, 2*
		ctx.closePath()
		ctx.fill()
	}
}
```
Приведенный ниже пример демонстрирует все режимы композиции, итерируя их и объединяя прямоугольник и круг. Полученный результат можно найти под исходным кодом.
```c++
property var operation : [
	'source-over', 'source-in', 'source-over',
	'source-atop', 'destination-over', 'destination-in',
	'destination-out', 'destination-atop', 'lighter',
	'copy', 'xor', 'qt-clear', 'qt-destination',
	'qt-multiply', 'qt-screen', 'qt-overlay', 'qt-darken',
	'qt-lighten', 'qt-color-dodge', 'qt-color-burn',
	'qt-hard-light', 'qt-soft-light', 'qt-difference',
	'qt-exclusion'
]

onPaint: {
	var ctx = getContext('2d')
	
	for(var i=0; i<operation.length; i++) {
		var dx = Math.floor(i%6)*100
		var dy = Math.floor(i/6)*100
		
		ctx.save()
		
		ctx.fillStyle = '#33a9ff'
		ctx.fillRect(10+dx,10+dy,60,60)
		ctx.globalCompositeOperation = root.operation[i]
		ctx.fillStyle = '#ff33a9'
		ctx.globalAlpha = 0.75
		ctx.beginPath()
		ctx.arc(60+dx, 60+dy, 30, 0, 2*Math.PI)
		ctx.closePath()
		ctx.fill()
		ctx.restore()
	}
}
```
![[QML canvas_7.png]]

## Пиксельные буферы

При работе с холстом вы можете получать из него пиксельные данные для чтения или манипулирования пикселями холста. Для чтения данных изображения используйте `createImageData(sw,sh)` или `getImageData(sx,sy,sw,sh)`.

Обе функции возвращают объект `ImageData` с параметрами `width`, `height` и `data` переменная данных . Переменная `data` содержит одномерный массив пиксельных данных, полученных в формате RGBA, где каждое значение изменяется в диапазоне от 0 до 255. Для установки пикселей на холсте можно использовать функцию `putImageData(imagedata, dx, dy)`.

Другим способом получения содержимого холста является сохранение данных в виде изображения. Этого можно добиться с помощью функций `Canvas.save(path)` или `toDataURL(mimeType)`, причем последняя функция возвращает URL-адрес изображения, который может быть использован для загрузки элементом `Image` 
```c++
import QtQuick

Rectangle {
	width: 240; height: 120

	Canvas {
		id: canvas
		x: 10; y: 10
		width: 100; height: 100
		
		property real hue: 0.0
		
		onPaint: {
			var ctx = getContext("2d")
			var x = 10 + Math.random(80)*80
			var y = 10 + Math.random(80)*80
			hue += Math.random()*0.1
			
			if(hue > 1.0) { hue -= 1 }
				ctx.globalAlpha = 0.7
				ctx.fillStyle = Qt.hsla(hue, 0.5, 0.5, 1.0)
				ctx.beginPath()
				ctx.moveTo(x+5,y)
				ctx.arc(x,y, x/10, 0, 360)
				ctx.closePath()
				ctx.fill()
		}
		
		MouseArea {
			anchors.fill: parent
			
			onClicked: {
				var url = canvas.toDataURL('image/png')
				print('image url=', url)
				image.source = url
			}
		}
	}
		
	Image {
		id: image
		x: 130; y: 10
		width: 100; height: 100
	}
	
	Timer {
		interval: 1000
		running: true
		triggeredOnStart: true
		repeat: true
		onTriggered: canvas.requestPaint()
	}
}
```
В нашем примере мы каждую секунду рисуем небольшой круг на левом холсте. Когда пользователь щелкает мышью на этой области, содержимое холста сохраняется и из него извлекается URL-адрес изображения. Затем это изображение отображается в правой части нашего примера.

## Краска для холста

В этом примере мы создадим небольшое приложение для рисования с использованием `Canvas`.

![[QML canvas_8.png]]

Для этого мы размещаем четыре цветовых квадрата в верхней части сцены с помощью позиционера строк. Цветовой квадрат - это простой прямоугольник, заполненный областью мыши для обнаружения щелчков.
```c++
Row {
	id: colorTools
	property color paintColor: "#33B5E5"
	
	anchors {
		horizontalCenter: parent.horizontalCenter
		top: parent.top
		topMargin: 8
	}
	
	spacing: 4
	
	Repeater {
		model: ["#33B5E5", "#99CC00", "#FFBB33", "#FF4444"]

		ColorSquare {
			required property var modelData
			color: modelData
			active: colorTools.paintColor == color
			
			onClicked: {
				colorTools.paintColor = color
			}
		}
	}
}
```
В массиве хранятся цвета и цвет краски. Когда пользователь щелкает в одном из квадратов, цвет квадрата присваивается свойству `paintColor` строки с именем `colorTools`.

Чтобы обеспечить отслеживание событий мыши на холсте, мы разместили `MouseArea` , охватывающую элемент холста, и подключили обработчики нажатия и изменения положения.
```c++
Canvas {
	id: canvas

	property real lastX: 0
	property real lastY: 0
	property color color: colorTools.paintColor
	
	anchors {
		left: parent.left
		right: parent.right
		top: colorTools.bottom
		bottom: parent.bottom
		margins: 8
	}
	
	onPaint: {
		var ctx = getContext('2d')
		ctx.lineWidth = 1.5
		ctx.strokeStyle = canvas.color
		ctx.beginPath()
		ctx.moveTo(lastX, lastY)
		lastX = area.mouseX
		lastY = area.mouseY
		ctx.lineTo(lastX, lastY)
		ctx.stroke()
	}
	
MouseArea {
	id: area
	
	anchors.fill: parent
	
	onPressed: {
		canvas.lastX = mouseX
		canvas.lastY = mouseY
	}
	
	onPositionChanged: {
		canvas.requestPaint()
		}
	}
}
```
При нажатии мыши начальная позиция мыши сохраняется в свойствах `lastX` и `lastY`. Каждое изменение положения мыши вызывает запрос на закрашивание холста, что приводит к вызову обработчика `onPaint`.

Чтобы окончательно нарисовать пользовательский штрих, в обработчике `onPaint` мы начинаем новый путь и перемещаемся в последнюю позицию. Затем мы получаем новую позицию из области мыши и рисуем линию выбранным цветом до новой позиции. Позиция мыши сохраняется как новая последняя позиция.

## Спирограф
В качестве основы мы используем пример спирографа (http://en.wikipedia.org/wiki/Spirograph) из проекта Mozilla. Оригинальный HTML5 был размещен в рамках учебника по работе с холстом (https://developer.mozilla.org/en-US/docs/Web/API/Canvas_API/Tutorial/Transformations) .

Нам нужно было изменить несколько строк:

> * Qt Quick требует объявления переменной, поэтому нам нужно было добавить несколько декларации `var`

```C++
for (var i=0; i<3; i++) {
	...
}
```
Мы адаптировали метод `draw` для получения объекта `Context2D`
```C++
function draw(ctx) {
	...
```
Нам пришлось адаптировать перевод для каждого спирографа из-за разных размеров
```C++
	ctx.translate(20+j*50,20+i*50);
```
Наконец, мы завершили работу над обработчиком `onPaint`. Внутри него мы получаем контекст и вызываем нашу функцию рисования.
```c++
onPaint: {
	var ctx = getContext("2d");
	draw(ctx);
}
```
В результате получилась портированная спирографика, работающая с использованием QML canvas.
![[QML canvas_9.png]]

Как видите, при отсутствии изменений в логике и относительно небольшом количестве изменений в самом коде перенос с HTML5 на QML вполне возможен.
## Светящиеся линии
Вот еще один более сложный порт от организации W3C. Сайт Оригинальные светящиеся линии (http://www.w3.org/TR/2dcontext/#examples) имеют несколько довольно приятных аспектов, что делает портирование более сложным.
![[QML canvas_10.png]]

```c++
<!DOCTYPE HTML>
<html lang="en">
<head>
	<title>Pretty Glowing Lines</title>
</head>

<body>
	<canvas width="800" height="450"></canvas>

<script>
var context = document.getElementsByTagName('canvas')[0].getCon
// initial start position
var lastX = context.canvas.width * Math.random();
var lastY = context.canvas.height * Math.random();
var hue = 0;
// closure function to draw
// a random bezier curve with random color with a glow effect
function line() {
	context.save();
	
	// scale with factor 0.9 around the center of canvas
	context.translate(context.canvas.width/2, context.canvas.he
	context.scale(0.9, 0.9);
	context.translate(-context.canvas.width/2, -context.canvas.
	context.beginPath();
	context.lineWidth = 5 + Math.random() * 10;
	
	// our start position
	context.moveTo(lastX, lastY);
	
	// our new end position
	lastX = context.canvas.width * Math.random();
	lastY = context.canvas.height * Math.random();
	
	// random bezier curve, which ends on lastX, lastY
	context.bezierCurveTo(context.canvas.width * Math.random(),
	context.canvas.height * Math.random(),
	context.canvas.width * Math.random(),
	context.canvas.height * Math.random(),
	lastX, lastY);
	
	// glow effect
	hue = hue + 10 * Math.random();
	context.strokeStyle = 'hsl(' + hue + ', 50%, 50%)';
	context.shadowColor = 'white';
	context.shadowBlur = 10;
	
	// stroke the curve
	context.stroke();
	context.restore();
}
					  
// call line function every 50msecs
setInterval(line, 50);
function blank() {
	// makes the background 10% darker on each call
	context.fillStyle = 'rgba(0,0,0,0.1)';
	context.fillRect(0, 0, context.canvas.width, context.canvas
}
					 
	// call blank function every 50msecs
	setInterval(blank, 40);
</script>
</body>
</html>
```
В HTML5 объект `Context2D` может рисовать на холсте в любое время. В QML он может указывать только внутри обработчика `onPaint`.  Таймер, используемый с помощью `setInterval`, вызывает в HTML5 обводку линии или очистку экрана. Из-за различий в обработке в QML невозможно просто вызвать эти функции, так как необходимо пройти через обработчик `onPaint`.

Также необходимо адаптировать цветовые представления. Давайте рассмотрим все изменения по порядку. 

Все начинается с элемента `canvas`. Для простоты мы просто используем элемент

Элемент `Canvas` в качестве корневого элемента нашего QML-файла.
```c++
import QtQuick

Canvas {
	id: canvas
	width: 800; height: 450
	...
}
```
Чтобы развязаться с прямым вызовом функций через `setInterval`, мы заменим вызовы `setInterval` двумя таймерами, которые будут запрашивать перерисовку. Таймер срабатывает через небольшой промежуток времени и позволяет нам выполнить некоторый код. Поскольку мы не можем указать функции `paint`, какую операцию мы хотим запустить, мы определяем для каждой операции флаг `bool`, запрашивающий операцию и вызывающий затем запрос перекраски.

Здесь приведен код для операции со строкой. Аналогично выполняется операция `blank`.
```c++
...
	property bool requestLine: false
	
	Timer {
		id: lineTimer
		interval: 40
		repeat: true
		triggeredOnStart: true
		
		onTriggered: {
			canvas.requestLine = true
			canvas.requestPaint()
		}
	}

	Component.onCompleted: {
		lineTimer.start()
	}
...
```
Теперь мы имеем представление о том, какую операцию (строку или пробел или даже обе) нам нужно выполнить в операции `onPaint`. При входе в обработчик `onPaint` для каждого запроса на закрашивание нам необходимо извлечь инициализацию переменной в элемент `canvas`.
```c++
Canvas {
	...
	
	property real hue: 0

	property real lastX: width * Math.random();
	property real lastY: height * Math.random();
	
	...
}
```
Теперь наша функция `paint` должна выглядеть следующим образом:
```c++
onPaint: {
	var context = getContext('2d')

	if(requestLine) {
		line(context)
		requestLine = false
	}
	
	if(requestBlank) {
		blank(context)
		requestBlank = false
	}
}
```
Для холста в качестве аргумента была извлечена линейная функция.
```c++
function line(context) {
	context.save();
	context.translate(canvas.width/2, canvas.height/2);
	context.scale(0.9, 0.9);
	context.translate(-canvas.width/2, -canvas.height/2);
	context.beginPath();
	context.lineWidth = 5 + Math.random() * 10;
	context.moveTo(lastX, lastY);
	lastX = canvas.width * Math.random();
	lastY = canvas.height * Math.random();
	context.bezierCurveTo(canvas.width * Math.random(),
	canvas.height * Math.random(),
	canvas.width * Math.random(),
	canvas.height * Math.random(),
	lastX, lastY);
	hue += Math.random()*0.1
	
	if(hue > 1.0) {
		hue -= 1
	}
	
	context.strokeStyle = Qt.hsla(hue, 0.5, 0.5, 1.0);
	// context.shadowColor = 'white';
	// context.shadowBlur = 10;
	context.stroke();
	context.restore();
}
```
Самым большим изменением стало использование функций QML `Qt.rgba()` и `Qt.hsla()`, что потребовало приведения значений к используемому в QML диапазону 0,0 ... 1,0.

То же самое относится и к функции `blank`.
```c++
function blank(context) {
	context.fillStyle = Qt.rgba(0,0,0,0.1)
	context.fillRect(0, 0, canvas.width, canvas.height);
}
```
Конечный результат будет выглядеть примерно так.
![[QML canvas_11.png]]














