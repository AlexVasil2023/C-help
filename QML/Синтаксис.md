# Синтаксис
**QML** - это декларативный язык, используемый для описания взаимосвязи объектов между собой. **QtQuick** - это фреймворк, построенный на **QML** для создания пользовательского интерфейса вашего приложения. Он разбивает пользовательский интерфейс на более мелкие элементы, которые могут быть объединены в компоненты. **QtQuick** описывает внешний вид и поведение этих элементов пользовательского интерфейса. Это описание пользовательского интерфейса может быть дополнено кодом **JavaScript** для обеспечения простой, но также и более сложной логики. С этой точки зрения, он повторяет схему **HTML-JavaScript**, но **QML** и **QtQuick** с самого начала были разработаны для описания пользовательских интерфейсов, а не текстовых документов. 
В своей простейшей форме **QtQuick** позволяет создавать иерархию элементов. Дочерние элементы наследуют систему координат от родительского. Координаты **x, y** всегда относятся к родительскому элементу.
Начнем с простого примера QML-файла, чтобы пояснить различия в синтаксисе.
```c++
// RectangleExample.qml

import QtQuick

// The root element is the Rectangle

Rectangle {
	// name this element root
	id: root
	
	// properties: <name>: <value>
	width: 120; height: 240
	
	// color property
	color: "#4A4A4A"
	
	// Declare a nested element (child of root)
	Image {
		id: triangle
		
		// reference the parent
		x: (parent.width - width)/2; y: 40
		source: 'assets/triangle_red.png'
	}
	
	// Another child of root
	Text {
		// un-named element
		
		// reference element by id
		y: triangle.y + triangle.height + 20
		
		// reference root element
		width: root.width
		
		color: 'white'
		horizontalAlignment: Text.AlignHCenter
		text: 'Triangle'
	}
}
```
* Оператор `import` импортирует модуль. Может быть добавлена необязательная версия в виде \<major>.\<minor>.
* Комментарии могут быть сделаны с помощью // для однострочных комментариев или /* \*/ для многострочных комментариев. Так же, как в C/C++ и JavaScript
* Каждый **QML**-файл должен иметь ровно один корневой элемент, как в **HTML** 
* Элемент объявляется по его типу, за которым следует { }
* Элементы могут иметь свойства, которые имеют вид **имя: значение**
* Доступ к произвольным элементам внутри QML-документа можно получить, используя их `id` (идентификатор без кавычек)
* Элементы могут быть вложенными, то есть родительский элемент может иметь дочерние элементы. Доступ к родительскому элементу можно получить с помощью ключевого слова `parent`
С помощью оператора `import` вы импортируете QML-модуль по имени. В Qt5 необходимо было указать мажорную и минорную версию (например, 2.15 ), в Qt6 это делать необязательно .

> Часто требуется обращаться к определенному элементу по `id` или к родительскому элементу, используя ключевое слово `parent`. Поэтому хорошей практикой является присвоение корневому элементу имени "root" с использованием `id: root`. Тогда вам не придется думать о том, как именовать корневой элемент в вашем QML-документе.
















