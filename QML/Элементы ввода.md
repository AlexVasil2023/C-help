# Элементы ввода
Мы уже использовали `MouseArea` качестве элемента ввода данных с помощью мыши. Далее мы сосредоточимся на вводе с клавиатуры. Начнем с элементов редактирования текста: `TextInput` и `TextEdit`.
## TextInput
`TextInput` позволяет пользователю ввести строку текста. Элемент поддерживает такие ограничения ввода, как `validator`, `inputMask` и `echoMode`.
```c++
// textinput.qml
import QtQuick
Rectangle {
	width: 200
	height: 80
	
	color: "linen"
	
	TextInput {
		id: input1
		x: 8; y: 8
		width: 96; height: 20
		focus: true
		text: "Text Input 1"
	}
	
	TextInput {
		id: input2
		x: 8; y: 36
		width: 96; height: 20
		text: "Text Input 2"
	}
}
```
Пользователь может щелкнуть внутри `TextInput`, чтобы изменить фокус. Для поддержки переключения фокуса с клавиатуры мы можем использовать прикрепленное свойство `KeyNavigation`.
```c++
// textinput2.qml
import QtQuick
Rectangle {
	width: 200
	height: 80
	
	color: "linen"
	
	TextInput {
		id: input1
		x: 8; y: 8
		width: 96; height: 20
		focus: true
		text: "Text Input 1"
		
		KeyNavigation.tab: input2
	}
	
	TextInput {
		id: input2
		x: 8; y: 36
		width: 96; height: 20
		text: "Text Input 2"
		
		KeyNavigation.tab: input1
	}
}
```
Присоединенное свойство `KeyNavigation` поддерживает предварительный набор навигационных клавиш, к которым привязывается идентификатор элемента, переключающий фокус при нажатии заданной клавиши.
Элемент ввода текста не имеет никакого визуального оформления, кроме мигающего курсора и вводимого текста. Для того чтобы пользователь мог распознать элемент как элемент ввода, ему необходимо некоторое визуальное оформление, например, простой прямоугольник. При размещении `TextInput` внутри элемента необходимо убедиться в том, что вы экспортируете основные свойства, которые должны быть доступны другим пользователям.
Мы перенесем этот кусок кода в наш собственный компонент под названием `TLineEditV1` для повторного использования.
```c++
// TLineEditV1.qml

import QtQuick
Rectangle {
	width: 96; height: input.height + 8
	color: "lightsteelblue"
	border.color: "gray"
	
	property alias text: input.text
	property alias input: input
	
	TextInput {
		id: input
		anchors.fill: parent
		anchors.margins: 4
		focus: true
	}
}
```

> Если вы хотите экспортировать `TextInput` полностью, то можно экспортировать элемент с помощью псевдонима свойства `input: input`. Первый вход - это имя свойства, а второй - идентификатор элемента.

Затем мы переписываем наш пример `KeyNavigation` с новым `TLineEditV1` компонент.
```c++
Rectangle {
	...
	TLineEditV1 {
		id: input1
		...
	}
	
	TLineEditV1 {
		id: input2
		...
	}
}
```
![[QML Edit 1.png]]
Попробуйте использовать клавишу табуляции для навигации. Вы увидите, что фокус не переключается на `input2`. Простого использования `focus: true` недостаточно. Проблема заключается в том, что при передаче фокуса на элемент `input2` элемент верхнего уровня внутри `TlineEditV1` (наш `Rectangle` ) получил фокус и не передал его на `TextInput`. Для предотвращения этого QML предлагает функцию `FocusScope`.
## FocusScope
Область видимости фокуса объявляет, что последний дочерний элемент с `focus: true` получает фокус, когда область видимости фокуса получает фокус. Таким образом, она пересылает фокус последнему запрашивающему фокус дочернему элементу. Создадим вторую версию нашего компонента `TLineEdit` под названием `TLineEditV2`, используя в качестве корневого элемента `focus scope`.
```c++
// TLineEditV2.qml
import QtQuick

FocusScope {
	width: 96; height: input.height + 8

	Rectangle {
		anchors.fill: parent
		color: "lightsteelblue"
		border.color: "gray"
	}
	
	property alias text: input.text
	property alias input: input
	
	TextInput {
		id: input
		anchors.fill: parent
		anchors.margins: 4
		focus: true
	}
}
```
Теперь наш пример выглядит следующим образом:
```c++
Rectangle {
	...
	
	TLineEditV2 {
		id: input1
		...
	}
	
	TLineEditV2 {
		id: input2
		...
	}
}
```
Теперь нажатие клавиши **tab** успешно переключает фокус между двумя компонентами, и фокусируется нужный дочерний элемент внутри компонента.

## TextEdit

`TextEdit` очень похож на `TextInput` и поддерживает многострочное поле редактирования текста. У него нет свойств ограничения текста, так как это зависит от запроса размера содержимого текста (`contentHeight`,
`contentWidth`). Мы также создаем собственный компонент под названием `TTextEdit` для обеспечения фона редактирования и использования области фокусировки для более точной переадресации фокуса.
```c++
// TTextEdit.qml
import QtQuick

FocusScope {
	width: 96; height: 96
	
	Rectangle {
		anchors.fill: parent
		color: "lightsteelblue"
		border.color: "gray"
	}
	
	property alias text: input.text
	property alias input: input
	
	TextEdit {
		id: input
		anchors.fill: parent
		anchors.margins: 4
		focus: true
	}
}
```
Вы можете использовать его подобно компоненту `TLineEdit`
```c++
// textedit.qml
import QtQuick

Rectangle {
	width: 136
	height: 120
	color: "linen"
	
	TTextEdit {
		id: input
		x: 8; y: 8
		width: 120; height: 104
		focus: true
		text: "Text Edit"
	}
}
```
![[QML Test Edit.png]]
## Элемент "Ключи"
Присоединенное свойство `Keys` позволяет выполнять код, основанный на нажатии определенных клавиш. Например, для перемещения и масштабирования квадрата мы можем подключиться к клавишам "вверх", "вниз", "влево" и "вправо", чтобы переместить элемент, и к клавишам "плюс" и "минус", чтобы масштабировать элемент.
```c++
// keys.qml
import QtQuick

DarkSquare {
	width: 400; height: 200
	
	GreenSquare {
		id: square
		x: 8; y: 8
	}
	
	focus: true
	
	Keys.onLeftPressed: square.x -= 8
	Keys.onRightPressed: square.x += 8
	Keys.onUpPressed: square.y -= 8
	Keys.onDownPressed: square.y += 8
	
	Keys.onPressed: function (event) {
		switch(event.key) {
			case Qt.Key_Plus:
				square.scale += 0.2
			break;
			case Qt.Key_Minus:
				square.scale -= 0.2
			break;
		}
	}
}
```
![[QML Square.png]]







